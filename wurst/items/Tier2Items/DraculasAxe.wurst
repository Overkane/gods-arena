package DraculasAxe
import RecipeItem
import ClosureEvents
import RubyAxe
import DraculasAxeBuff
import TooltipFactory
import MaskOfDeath
import MapIcons
import ChannelAbilityPreset
import CustomStatSystem
import ItemDefinitionExtension

constant LIFESTEAL_ID = compiletime(ABIL_ID_GEN.next())
constant BONUS_LIFESTEAL = 40.
constant DURATION = 8.
constant MANA_COST = 120
constant COOLDOWN = 40

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Bloodthirst", 
  "Increases physical lifesteal by " + BONUS_LIFESTEAL.round().colorPerc(COLOR_GOLD_STR) + ". " 
    + getDurationString(DURATION.round()),
    COOLDOWN, MANA_COST)

public constant DRACULAS_AXE_ITEM = compiletime(new RecipeItem()
..setName("Dracula's Axe")
..setInterfaceIcon(MapIcons.bTNDraculasAxe)
..setActivelyUsed(true)
..addBonus(Bonus.DAMAGE, 60)
..addBonus(Bonus.ATTACKSPEED, -30)
..addBonus(Bonus.STRENGTH, 25)
..addCustomStat(CustomUnitStats.ATTACK_POWER, 15)
..addCustomStat(CustomUnitStats.LIFESTEAL, 21)
..setAbilityWithCooldownGroup(LIFESTEAL_ID)
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant DRACULAS_AXE_RECIPE = new ItemRecipe()
..gold(470)
..setRequiredItems(RUBY_AXE_ITEM, RUBY_AXE_ITEM, MASK_OF_DEATH_ITEM)
..setResultItem(DRACULAS_AXE_ITEM)

init
  EventListener.onCast(LIFESTEAL_ID) (unit caster) ->
    new DraculasAxeBuff(DURATION, BONUS_LIFESTEAL/100).apply(caster)

@compiletime function genItemAbils()
  new ChannelAbilityPreset(LIFESTEAL_ID, 1, true)
  ..setManaCost(1, MANA_COST)
  ..setCooldown(1, COOLDOWN.toReal())
