package Corruptor
import TooltipFactory
import PassiveAbilityPreset
import RecipeItem
import ArmorBreakBuff
import CustomStatSystem
import Spear
import SteelSword
import Emerald

constant ARMOR_BREAK_VALUE = 7.
constant ARMOR_BREAK_DURATION = 8.

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.PASSIVE,
  "Armor Break", 
  "successful basic attacks decrease enemy armor by {0}. ".format(ARMOR_BREAK_VALUE.round().color(COLOR_GOLD_STR))
    + getDurationString(ARMOR_BREAK_DURATION.round())
    + " Doesn't stack.".color(IMPORTANT_COLOR))

public constant CORRUPTOR_ITEM = compiletime(new RecipeItem()
..setName("Corruptor")
..setInterfaceIcon(Icons.bTNTheBlackArrow)
..setGoldCost(230)
..addBonus(Bonus.DAMAGE, 60)
..addBonus(Bonus.AGILITY, 12)
..addCustomStat(CustomUnitStats.ATTACK_CRIT_CHANCE, 8)
..addCustomStat(CustomUnitStats.ATTACK_CRIT_POWER, 40)
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant CORRUPTOR_RECIPE = new ItemRecipe()
..gold(480)
..setRequiredItems(SPEAR_ITEM, EMERALD_ITEM, STEEL_SWORD_ITEM)
..setResultItem(CORRUPTOR_ITEM)

init
  DamageEvent.addListener(99) -> 
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if DamageEvent.getType() == DamageType.ATTACK 
      and sunit.hasItemById(CORRUPTOR_ITEM.getNewId()) and sunit.isEnemyOf(tunit)
      
      new ArmorBreakBuff(ARMOR_BREAK_DURATION, ARMOR_BREAK_VALUE).apply(tunit)


