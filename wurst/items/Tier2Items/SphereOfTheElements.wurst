package SphereOfTheElements
import ItemObjEditing
import RecipeItem
import ChannelAbilityPreset
import MapIcons
import ClosureEvents
import SoundUtils
import ElementalStone
import TooltipFactory
import ItemDefinitionExtension

constant SPHERE_OF_THE_ELEMENTS_ID = compiletime(ABIL_ID_GEN.next())
constant MANA_COST = 200
constant COOLDOWN = 100

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Refresh", 
  "refreshes all abilities of the owner.",
  COOLDOWN, MANA_COST)

public constant SPHERE_OF_THE_ELEMENTS_ITEM = compiletime(new RecipeItem()
  ..setName("Sphere of the Elements")
  ..setInterfaceIcon(MapIcons.bTNShpereOfVoid)
  ..setActivelyUsed(true)
  ..addBonus(Bonus.STRENGTH, 30)
  ..addBonus(Bonus.AGILITY, 30)
  ..addBonus(Bonus.INTELLIGENCE, 30)
  ..setAbilityWithCooldownGroup(SPHERE_OF_THE_ELEMENTS_ID)
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant SPHERE_OF_THE_ELEMENTS_RECIPE = new ItemRecipe()
..gold(420)
..lumber(1)
..setRequiredItems(ELEMENTAL_STONE_ITEM, ELEMENTAL_STONE_ITEM)
..setResultItem(SPHERE_OF_THE_ELEMENTS_ITEM)

init
  EventListener.onCast(SPHERE_OF_THE_ELEMENTS_ID) (unit caster) ->
    new SoundDefinition(Sounds.wandOfIllusionTarget1).playForPlayer(caster.getOwner())
    caster.resetCooldown()

@compiletime function genItemAbils()
  new ChannelAbilityPreset(SPHERE_OF_THE_ELEMENTS_ID, 1, true)
  ..setManaCost(1, MANA_COST)
  ..setCooldown(1, COOLDOWN.toReal())
