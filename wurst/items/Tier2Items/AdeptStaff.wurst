package AdeptStaff
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import ApprenticeStaff
import FoliantOfWizardry
import TooltipFactory
import CustomStatSystem
import MapIcons

constant FIREBOLT_ID = compiletime(ABIL_ID_GEN.next())
constant FIREBOLT_DAMAGE = 350
constant FIREBOLT_STUN = 0.5

constant SPELL_POWER = 25.

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Fire Bolt", 
  "casts a firebolt which deals " + FIREBOLT_DAMAGE.color(COLOR_GOLD_STR)
    + " damage and stuns target for " + FIREBOLT_STUN.color(COLOR_GOLD_STR) + " seconds.")

public constant ADEPT_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Adept Staff")
..setInterfaceIcon(MapIcons.btnMage2)
..setActivelyUsed(true)
..addBonus(Bonus.INTELLIGENCE, 25)
..addBonus(Bonus.MANAREGEN, 2)
..setAbilities(commaList(FIREBOLT_ID))
..setCooldownGroup(FIREBOLT_ID.toRawCode())
..addCustomStat(CustomUnitStats.SPELL_POWER, SPELL_POWER)
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant ADEPT_STAFF_RECIPE = new ItemRecipe()
..gold(265)
..setRequiredItems(APPRENTICE_STAFF_ITEM, FOLIANT_OF_WIZ_ITEM)
..setResultItem(ADEPT_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionFireBoltcreep(FIREBOLT_ID)
  ..setDamage(1, FIREBOLT_DAMAGE.toReal())
  ..setDurationNormal(1, FIREBOLT_STUN)
  ..setDurationHero(1, FIREBOLT_STUN)
  ..setManaCost(1, 100)
  ..setCooldown(1, 16)