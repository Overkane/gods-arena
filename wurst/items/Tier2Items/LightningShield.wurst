package LightningShield
import RecipeItem
import CustomStatSystem
import OrderIds
import ChannelAbilityPreset
import IronShield
import SimpleArmor
import RingOfSpellLifesteal
import TooltipFactory
import DummyStatCaster
import ClosureEvents

constant BLOCK_ID = compiletime(ABIL_ID_GEN.next())
constant BLOCK_CHANCE = 75
constant BLOCK_VALUE = 40

constant LIGHTNING_SHIELD_ID = compiletime(ABIL_ID_GEN.next())
constant LIGHTNING_SHIELD_DUMMY_ID = compiletime(ABIL_ID_GEN.next())
constant DAMAGE_PER_SECOND = 60
constant DURATION = 10.
constant MANA_COST = 145
constant COOLDOWN = 30

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Lightning Shield", 
  "casts lightning shield on self, which deals {0} damage per second. ".format(
      DAMAGE_PER_SECOND.color(COLOR_GOLD_STR)
    )
    + getDurationString(DURATION.round()),
    COOLDOWN, MANA_COST)

constant ABIL_TOOLTIP2 = itemAbilityTooltip(AbilityType.PASSIVE,
  "Block", 
  BLOCK_CHANCE.colorPerc(COLOR_GOLD_STR) + " chance to block " + BLOCK_VALUE.color(COLOR_GOLD_STR) + " damage.")

public constant LIGHTNING_SHIELD_ITEM = compiletime(new RecipeItem()
..setName("Lightning Shield")
..setInterfaceIcon(Icons.bTNLightningShield)
..setActivelyUsed(true)
..addBonus(Bonus.LIFE, 200)
..addBonus(Bonus.ARMOR, 5)
..addBonus(Bonus.LIFEREGEN, 3)
..addBonus(Bonus.INTELLIGENCE, 10)
..addCustomStat(CustomUnitStats.SPELL_LIFESTEAL, 20)
..setAbilities(commaList(LIGHTNING_SHIELD_ID, BLOCK_ID))
..setCooldownGroup(LIGHTNING_SHIELD_ID.toRawCode())
..generateTooltip(ABIL_TOOLTIP + "\n\n" + ABIL_TOOLTIP2)
)..register()

public constant LIGHTNING_SHIELD_RECIPE = new ItemRecipe()
..gold(405)
..setRequiredItems(IRON_SHIELD_ITEM, SIMPLE_ARMOR_ITEM, RING_OF_SPELL_LIFESTEAL_ITEM )
..setResultItem(LIGHTNING_SHIELD_ITEM)

init 
  EventListener.onCast(LIGHTNING_SHIELD_ID) (unit caster) ->
    new DummyStatCaster.castTarget(LIGHTNING_SHIELD_DUMMY_ID, 1, OrderIds.lightningshield, caster)

@compiletime function genAbility()
  new ChannelAbilityPreset(LIGHTNING_SHIELD_ID, 1, true)
  ..setManaCost(1, MANA_COST)
  ..setCooldown(1, COOLDOWN.toReal())

  new AbilityDefinitionLightningShieldcreep(LIGHTNING_SHIELD_DUMMY_ID)
  ..setDummyAbility()
  ..setDamageperSecond(1, DAMAGE_PER_SECOND.toReal())
  ..setDurationNormal(1, DURATION)
  ..setDurationHero(1, DURATION)