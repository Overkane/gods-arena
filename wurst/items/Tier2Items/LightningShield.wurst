package LightningShield
import RecipeItem
import Icons
import PassiveAbilityPreset
import OrderIds
import DamageEvent
import IronShield
import SimpleArmor
import TooltipFactory
import DummyStatCaster

constant BLOCK_ID = compiletime(ABIL_ID_GEN.next())
constant BLOCK_CHANCE = 75
constant BLOCK_VALUE = 40

constant LIGHTNING_SHIELD_ID = compiletime(ABIL_ID_GEN.next())
constant CHAIN_LIGHTNING_ID = compiletime(ABIL_ID_GEN.next())
constant CHAIN_LIGHTNING_CHANCE = 10
constant CHAIN_LIGHTNING_DAMAGE = 125
constant CHAIN_LIGHTNING_NUMBER_OF_UNITS = 3

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.PASSIVE,
  "Block", 
  BLOCK_CHANCE.colorPerc(COLOR_GOLD) + " chance to block " + BLOCK_VALUE.color(COLOR_GOLD) + " damage.")

constant ABIL_TOOLTIP2 = itemAbilityTooltip(AbilityType.PASSIVE,
  "Lightning Shield", 
  CHAIN_LIGHTNING_CHANCE.colorPerc(COLOR_GOLD) + " chance to cast chain lightning on attacker, which deals " 
  + CHAIN_LIGHTNING_DAMAGE.color(COLOR_GOLD) + " damage and jumps between " + CHAIN_LIGHTNING_NUMBER_OF_UNITS.color(COLOR_GOLD) + " units.")

public constant LIGHTNING_SHIELD_ITEM = compiletime(new RecipeItem()
..setName("Lightning Shield")
..setInterfaceIcon(Icons.bTNLightningShield)
..addBonus(Bonus.LIFE, 300)
..addBonus(Bonus.ARMOR, 5)
..setAbilities(commaList(BLOCK_ID, LIGHTNING_SHIELD_ID))
..generateTooltip(ABIL_TOOLTIP + "\n\n" + ABIL_TOOLTIP2)
)..register()

public constant LIGHTNING_SHIELD_RECIPE = new ItemRecipe()
..gold(210)
..setRequiredItems(SIMPLE_ARMOR_ITEM, IRON_SHIELD_ITEM)
..setResultItem(LIGHTNING_SHIELD_ITEM)

init 
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if tunit.hasItemById(LIGHTNING_SHIELD_ITEM.getNewId()) and not tunit.isIllusion() and DamageEvent.getType() == DamageType.ATTACK and sunit.isEnemyOf(tunit)
      if CHAIN_LIGHTNING_CHANCE >= GetRandomInt(1, 100)
        new DummyStatCaster()
        ..origin(tunit.getPos())
        ..caster(tunit)
        ..castTarget(CHAIN_LIGHTNING_ID, 1, OrderIds.chainlightning, sunit)

@compiletime function genAbility()
  new AbilityDefinitionHardenedSkin(BLOCK_ID)
  ..setChancetoReduceDamage(1, BLOCK_CHANCE.toReal())
  ..setIgnoredDamage(1, BLOCK_VALUE.toReal())
  ..setMinimumDamage(1, 1)
  ..setCheckDependencies(false)

  new PassiveAbilityPreset(LIGHTNING_SHIELD_ID, 1)

  new AbilityDefinitionFarseerChainLightning(CHAIN_LIGHTNING_ID)
  ..setDummyAbility()
  ..setDamageperTarget(1, CHAIN_LIGHTNING_DAMAGE.toReal())
  ..setNumberofTargetsHit(1, CHAIN_LIGHTNING_NUMBER_OF_UNITS)
  ..presetDamageReductionperTarget(lvl -> 0)
    