package WizardStaff
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import AdeptStaff
import MagesSword
import TooltipFactory
import CustomStatSystem
import MapIcons
import CooldownGroups

constant FIREBOLT_ID = compiletime(ABIL_ID_GEN.next())
constant FIREBOLT_DAMAGE = 500
constant FIREBOLT_STUN = 0.75
constant MANA_COST = 125
constant COOLDOWN = 16

constant SPELL_POWER = 35.

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Fire Bolt", 
  "fires firebolt which deals " + FIREBOLT_DAMAGE.color(COLOR_GOLD_STR)
  + " damage and stuns target for " + FIREBOLT_STUN.color(COLOR_GOLD_STR) + " seconds.",
  COOLDOWN, MANA_COST)

public constant WIZARD_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Wizard Staff")
..setInterfaceIcon(MapIcons.btnMage3)
..setActivelyUsed(true)
..addBonus(Bonus.DAMAGE, 30)
..addBonus(Bonus.INTELLIGENCE, 30)
..addBonus(Bonus.MANAREGEN, 3)
..addCustomStat(CustomUnitStats.SPELL_POWER, SPELL_POWER)
..addCustomStat(CustomUnitStats.SPELL_CRIT_CHANCE, 15)
..addCustomStat(CustomUnitStats.SPELL_CRIT_POWER, 60)
..setAbilities(commaList(FIREBOLT_ID))
..setCooldownGroup(FIREBOLT_COOLDOWN_GROUP_ID.toRawCode())
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant WIZARD_STAFF_RECIPE = new ItemRecipe()
..gold(665)
..setRequiredItems(ADEPT_STAFF_ITEM, MAGES_SWORD_ITEM)
..setResultItem(WIZARD_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionFireBoltcreep(FIREBOLT_ID)
  ..setDamage(1, FIREBOLT_DAMAGE.toReal())
  ..setDurationNormal(1, FIREBOLT_STUN)
  ..setDurationHero(1, FIREBOLT_STUN)
  ..setManaCost(1, MANA_COST)
  ..setCooldown(1, COOLDOWN.toReal())