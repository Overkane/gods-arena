package ArchmageRobe
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import TooltipFactory
import MapIcons
import MantleOfProtection
import RingOfSpellLifesteal
import CustomStatSystem

constant MANA_SHIELD_ID = compiletime(ABIL_ID_GEN.next())
constant MANA_SHIELD_ABSORB = 2
constant MANA_SHIELD_DURATION = 20

constant SPELL_LIFESTEAL_VALUE = 20.

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Mana Shield", 
  "activates a shield that absorbs " + MANA_SHIELD_ABSORB.color(COLOR_GOLD_STR) + " damage per point of mana. Lasts " + MANA_SHIELD_DURATION.color(COLOR_GOLD_STR) + " seconds.")


public constant ARCHMAGE_ROBE_ITEM = compiletime(new RecipeItem()
..setName("Archmage Robe")
..setInterfaceIcon(MapIcons.bTNSuperiorMageRobe)
..setActivelyUsed(true)
..addBonus(Bonus.LIFE, 350)
..addBonus(Bonus.LIFEREGEN, 4)
..addBonus(Bonus.ARMOR, 6)
..addBonus(Bonus.MANA, 350)
..addBonus(Bonus.MANAREGEN, 2)
..addBonus(Bonus.INTELLIGENCE, 15)
..setAbilities(commaList(MANA_SHIELD_ID))
..addCustomStat(CustomUnitStats.SPELL_LIFESTEAL, SPELL_LIFESTEAL_VALUE)
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant ARCHMAGE_ROBE_RECIPE = new ItemRecipe()
..gold(390)
..setRequiredItems(MANTLE_OF_PROTECTION_ITEM, RING_OF_SPELL_LIFESTEAL_ITEM)
..setResultItem(ARCHMAGE_ROBE_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionManaShieldCreep(MANA_SHIELD_ID)
  ..setDamageAbsorbed(1, 1)
  ..setManaperHitPoint(1, MANA_SHIELD_ABSORB.toReal())
  ..setDurationHero(1, MANA_SHIELD_DURATION.toReal())
  ..setManaCost(1, 25)
  ..setCooldown(1, 30)
