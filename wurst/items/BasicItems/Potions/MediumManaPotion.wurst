package MediumManaPotion
import ItemObjEditing
import ObjectIdGenerator
import AbilityObjEditing
import RecipeItem
import ObjectIds
import ClosureEvents
import SmallManaPotion
import TooltipFactory
import MapIcons

constant MANA_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_MP = 450
constant PERC_MP = 25

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Mana Restoration", 
  "Restores "+ FLAT_MP.color(COLOR_GOLD) +" or "+ PERC_MP.colorPerc(COLOR_GOLD) +" mp, depending on which value is bigger.")

public constant M_MANA_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Medium Mana Potion")
  ..setInterfaceIcon(MapIcons.bTNMediumManaPotion)
  ..setGoldCost(75)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(MANA_ID.toRawCode())
  ..setCooldownGroup(MANA_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(MANA_ID) (unit caster) ->
    let percMP = caster.getMaxMana() * PERC_MP/100
    
    if FLAT_MP > percMP
      caster.addMana(FLAT_MP.toReal())
    else
      caster.addMana(percMP)

@compiletime function genItemAbils()
  new AbilityDefinitionItemManaRestoreLesser(MANA_ID)
  ..presetManaPointsGained(lvl -> 0)
  ..presetCooldown(lvl -> 15)

