package SmallManaPotion
import ItemObjEditing
import ObjectIdGenerator
import AbilityObjEditing
import RecipeItem
import ObjectIds
import ClosureEvents
import TooltipFactory

public constant MANA_COOLDOWN_GROUP_ID = compiletime(ABIL_ID_GEN.next())

constant MANA_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_MP = 225
constant PERC_MP = 15

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Mana Restoration", 
  "Restores "+ FLAT_MP.color(COLOR_GOLD) +" or "+ PERC_MP.colorPerc(COLOR_GOLD) +" mp, depending on which value is bigger.")

public constant S_MANA_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Small Mana Potion")
  ..setInterfaceIcon("ReplaceableTextures/CommandButtons/BTNSmallManaPotion.blp")
  ..setGoldCost(35)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(MANA_ID.toRawCode())
  ..setCooldownGroup(MANA_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(MANA_ID) (unit caster) ->
    let percMP = caster.getMaxMana() * PERC_MP/100
    
    if FLAT_MP > percMP
      caster.addMana(FLAT_MP.toReal())
    else
      caster.addMana(percMP)

@compiletime function genItemAbils()
  new AbilityDefinitionItemManaRestoreLesser(MANA_ID)
  ..presetManaPointsGained(lvl -> 0)
  ..presetCooldown(lvl -> 10)

