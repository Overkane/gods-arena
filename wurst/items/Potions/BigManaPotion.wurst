package BigManaPotion
import ItemObjEditing
import RecipeItem
import ClosureEvents
import CooldownGroups
import TooltipFactory
import MapIcons
import ChannelAbilityPreset
import Abilities

constant MANA_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_MP = 1000
constant PERC_MP = 45
constant COOLDOWN = 24

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Mana Restoration", 
  "Restores "+ FLAT_MP.color(COLOR_GOLD_STR) +" or "+ PERC_MP.colorPerc(COLOR_GOLD_STR) +" mp, depending on which value is bigger.",
  COOLDOWN)

public constant B_MANA_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Big Mana Potion")
  ..setInterfaceIcon(MapIcons.bTNBigManaPotion)
  ..setGoldCost(165)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(MANA_ID.toRawCode())
  ..setCooldownGroup(MANA_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(MANA_ID) (unit caster) ->
    let percMP = caster.getMaxMana() * PERC_MP/100
    
    if FLAT_MP > percMP
      caster.addMana(FLAT_MP.toReal())
    else
      caster.addMana(percMP)

@compiletime function genItemAbils()
  new ChannelAbilityPreset(MANA_ID, 1, true)
  ..setArtCaster(Abilities.aImaTarget)
  ..presetCooldown(lvl -> COOLDOWN.toReal())

