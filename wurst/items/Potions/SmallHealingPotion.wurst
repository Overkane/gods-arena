package SmallHealingPotion
import ItemObjEditing
import ObjectIdGenerator
import AbilityObjEditing
import RecipeItem
import ObjectIds
import ClosureEvents
import TooltipFactory
import MapIcons

public constant HEALING_COOLDOWN_GROUP_ID = compiletime(ABIL_ID_GEN.next())

constant HEAL_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_HEAL = 400
constant PERC_HEAL = 25

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Heal", 
  "Heals "+ FLAT_HEAL.color(COLOR_GOLD) +" or "+ PERC_HEAL.colorPerc(COLOR_GOLD) +" hp, depending on which value is bigger.")

public constant S_HEALING_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Small Healing Potion")
  ..setInterfaceIcon(MapIcons.bTNSmallHealingPotion)
  ..setGoldCost(25)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(HEAL_ID.toRawCode())
  ..setCooldownGroup(HEALING_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(HEAL_ID) (unit caster) ->
    let percHeal = caster.getMaxHP() * PERC_HEAL/100
    
    if FLAT_HEAL > percHeal
      caster.addHP(FLAT_HEAL.toReal())
    else
      caster.addHP(percHeal)

@compiletime function genItemAbils()
  new AbilityDefinitionItemHealLesser(HEAL_ID)
  ..presetHitPointsGained(lvl -> 0)
  ..presetCooldown(lvl -> 10)

