package DivinePotion
import RecipeItem
import TooltipFactory
import ClosureEvents
import ChannelAbilityPreset
import DivinePotionBuff
import ItemDefinitionExtension

constant DIVINITY_ID = compiletime(ABIL_ID_GEN.next())
constant STAT_INCREASE_PERC = 30.
constant DURATION = 30.
constant COOLDOWN = 60

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Divinity", 
  "Increases all attributes by " + STAT_INCREASE_PERC.colorPerc(COLOR_GOLD_STR) + " based on base attribute value. " 
    + getDurationString(DURATION.round()),
    COOLDOWN)

public constant DIVINE_POTION_ITEM = compiletime(new RecipeItem('vamp')
  ..setName("Divine Potion")
  ..setInterfaceIcon(Icons.bTNPotionOfRestoration)
  ..setGoldCost(300)
  ..setLumberCost(1)
  ..generateTooltip(ABIL_TOOLTIP)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setNumberofCharges(1)
  ..setMaxStack(1)
  ..setAbilityWithCooldownGroup(DIVINITY_ID)
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(DIVINITY_ID) (unit caster) ->
    new DivinePotionBuff(DURATION, STAT_INCREASE_PERC/100).apply(caster)

@compiletime function gen()
  new ChannelAbilityPreset(DIVINITY_ID, 1, true)
  ..setCooldown(1, COOLDOWN.toReal())
