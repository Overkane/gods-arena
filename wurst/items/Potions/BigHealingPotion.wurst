package BigHealingPotion
import ItemObjEditing
import ObjectIdGenerator
import AbilityObjEditing
import RecipeItem
import ObjectIds
import ClosureEvents
import CooldownGroups
import TooltipFactory
import MapIcons
import CustomHeal

constant HEAL_ID = compiletime(ABIL_ID_GEN.next())
constant FLAT_HEAL = 1600
constant PERC_HEAL = 60
constant COOLDOWN = 20

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Heal", 
  "Heals "+ FLAT_HEAL.color(COLOR_GOLD_STR) +" or "+ PERC_HEAL.colorPerc(COLOR_GOLD_STR) +" hp, depending on which value is bigger.",
  COOLDOWN)

public constant B_HEALING_POT_ITEM = compiletime(new RecipeItem()
  ..setName("Big Healing Potion")
  ..setInterfaceIcon(MapIcons.bTNBigHealingPotion)
  ..setGoldCost(145)
  ..setNumberofCharges(1)
  ..setMaxStack(5)
  ..setActivelyUsed(true)
  ..setPerishable(true)
  ..setAbilities(HEAL_ID.toRawCode())
  ..setCooldownGroup(HEALING_COOLDOWN_GROUP_ID.toRawCode())
  ..generateTooltip(ABIL_TOOLTIP)
)..register()

init 
  EventListener.onCast(HEAL_ID) (unit caster) ->
    let percHeal = caster.getMaxHP() * PERC_HEAL/100
    
    if FLAT_HEAL > percHeal
      neutralHeal(caster, FLAT_HEAL.toReal())
    else
      neutralHeal(caster, percHeal)

@compiletime function genItemAbils()
  new AbilityDefinitionItemHealLesser(HEAL_ID)
  ..presetHitPointsGained(lvl -> 0)
  ..presetCooldown(lvl -> COOLDOWN.toReal())

