package Mace
import Icons
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import IronAxe
import TooltipFactory

constant BASH_ID = compiletime(ABIL_ID_GEN.next())
constant BASH_CHANCE = 15
constant BASH_DURATION = 0.4
constant BASH_DAMAGE = 25

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.PASSIVE,
  "Bash", 
  "gives " + BASH_CHANCE.colorPerc(COLOR_GOLD) + " chance to bash on attack for " + BASH_DURATION.color(COLOR_GOLD) + " seconds with " 
  + BASH_DAMAGE.color(COLOR_GOLD) + " bonus damage.")

public constant MACE_ITEM = compiletime(new RecipeItem()
..setName("Mace")
..setInterfaceIcon(Icons.bTNHammer)
..addBonus(Bonus.DAMAGE, 50)
..addBonus(Bonus.ATTACKSPEED, -15)
..setAbilities(commaList(BASH_ID))
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant MACE_RECIPE = new ItemRecipe()
..gold(90)
..setRequiredItems(IRON_AXE_ITEM, IRON_AXE_ITEM)
..setResultItem(MACE_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionBashcreep(BASH_ID)
  ..setChancetoBash(1, BASH_CHANCE.toReal())
  ..setDamageBonus(1, BASH_DAMAGE.toReal())
  ..setDurationHero(1, BASH_DURATION)
  ..setDurationNormal(1, BASH_DURATION)
