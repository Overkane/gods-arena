package Fury
import TooltipFactory
import PassiveAbilityPreset
import RecipeItem
import Buff
import FuryBuff
import initlater Striker
import GlovesOfHaste
import PowerGloves
import Emerald

constant FURY_ID = compiletime(ABIL_ID_GEN.next())
constant FURY_MAX_AS = 80.
constant FURY_BONUS_AS = 5.
constant FURY_DURATION = 5.

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.PASSIVE,
  "Fury", 
  "each attack on the same target increases attack speed by " + FURY_BONUS_AS.round().colorPerc(COLOR_GOLD_STR)
    + " up to a maximum of " + FURY_MAX_AS.round().color(COLOR_GOLD_STR) + ". "
    + "When attack target is changed or "+ FURY_DURATION.round().color(COLOR_GOLD_STR) + " seconds passed, the effect disappears and all accumulated attack speed bonus is lost. " 
    + "Multiple items don't stack.".color(IMPORTANT_COLOR))

public constant FURY_ITEM = compiletime(new RecipeItem()
..setName("Fury")
..setInterfaceIcon(Icons.bTNAdvancedUnholyStrength)
..addBonus(Bonus.ATTACKSPEED, 30)
..addBonus(Bonus.AGILITY, 10)
..setAbilities(commaList(FURY_ID))
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant FURY_RECIPE = new ItemRecipe()
..gold(250)
..setRequiredItems(GLOVES_OF_HASTE_ITEM, POWER_GLOVES_ITEM, EMERALD_ITEM)
..setResultItem(FURY_ITEM)

init
  FuryBuff.maxAS = FURY_MAX_AS

  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if DamageEvent.getType() == DamageType.ATTACK 
      and sunit.hasItemById(FURY_ITEM.getNewId()) and not sunit.hasItemById(STRIKER_ITEM.getNewId())
      and not sunit.isIllusion() and sunit.isEnemyOf(tunit)
      
      if FuryBuff.buffTargets[sunit.getIndex()] != tunit
        sunit.removeBuffById(FuryBuff.typeId)
        FuryBuff.buffTargets[sunit.getIndex()] = tunit

      new FuryBuff(FURY_DURATION, FURY_BONUS_AS).apply(sunit)

@compiletime function genItemAbils()
  new PassiveAbilityPreset(FURY_ID, 1)
