package ApprenticeStaff
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import SpiritMask
import Sapphire
import WoodenStaff
import TooltipFactory
import MapIcons
import CustomStatSystem
import ObjectIds
import CooldownGroups

constant FIREBOLT_ID = compiletime(ABIL_ID_GEN.next())
constant FIREBOLT_DAMAGE = 200
constant FIREBOLT_STUN = 1.
constant SPELL_POWER = 10.
constant MANA_COST = 75
constant COOLDOWN = 16

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Fire Bolt", 
  "fires firebolt which deals " + FIREBOLT_DAMAGE.color(COLOR_GOLD_STR)
  + " damage and stuns target for " + FIREBOLT_STUN..round().color(COLOR_GOLD_STR) + " seconds.",
  COOLDOWN, MANA_COST)

public constant APPRENTICE_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Apprentice Staff")
..setInterfaceIcon(MapIcons.btnMage1)
..setActivelyUsed(true)
..addBonus(Bonus.INTELLIGENCE, 10)
..addBonus(Bonus.MANAREGEN, 0.75)
..addCustomStat(CustomUnitStats.SPELL_POWER, SPELL_POWER)
..setAbilities(commaList(FIREBOLT_ID))
..setCooldownGroup(FIREBOLT_COOLDOWN_GROUP_ID.toRawCode())
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant APPRENTICE_STAFF_RECIPE = new ItemRecipe()
..gold(290)
..setRequiredItems(SPIRIT_MASK_ITEM, SAPPHIRE_ITEM, WOODEN_STAFF_ITEM)
..setResultItem(APPRENTICE_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionFireBolt(FIREBOLT_ID)
  ..setDamage(1, FIREBOLT_DAMAGE.toReal())
  ..setDurationNormal(1, FIREBOLT_STUN)
  ..setDurationHero(1, FIREBOLT_STUN/2)
  ..setManaCost(1, MANA_COST)
  ..setCooldown(1, COOLDOWN.toReal())
