package ApprenticeStaff
import RecipeItem
import AbilityObjEditing
import ObjectIdGenerator
import ObjectIds
import SpiritMask
import Sapphire
import TooltipFactory
import MapIcons

constant FIREBOLT_ID = compiletime(ABIL_ID_GEN.next())
constant FIREBOLT_DAMAGE = 200
constant FIREBOLT_STUN = 0.5

constant ABIL_TOOLTIP = itemAbilityTooltip(AbilityType.ACTIVE,
  "Fire Bolt", 
  "fires firebolt which deals " + FIREBOLT_DAMAGE.color(COLOR_GOLD_STR)
  + " damage and stuns target for " + FIREBOLT_STUN.color(COLOR_GOLD_STR) + " seconds.")

public constant APPRENTICE_STAFF_ITEM = compiletime(new RecipeItem()
..setName("Apprentice Staff")
..setInterfaceIcon(MapIcons.btnMage1)
..setActivelyUsed(true)
..addBonus(Bonus.INTELLIGENCE, 10)
..addBonus(Bonus.MANAREGEN, 1)
..setAbilities(commaList(FIREBOLT_ID))
..setCooldownGroup(FIREBOLT_ID.toRawCode())
..generateTooltip(ABIL_TOOLTIP)
)..register()

public constant APPRENTICE_STAFF_RECIPE = new ItemRecipe()
..gold(65)
..setRequiredItems(SPIRIT_MASK_ITEM, SAPPHIRE_ITEM)
..setResultItem(APPRENTICE_STAFF_ITEM)

@compiletime function genItemAbils()
  new AbilityDefinitionFireBoltcreep(FIREBOLT_ID)
  ..setDamage(1, FIREBOLT_DAMAGE.toReal())
  ..setDurationNormal(1, FIREBOLT_STUN)
  ..setDurationHero(1, FIREBOLT_STUN)
  ..setManaCost(1, 75)
  ..setCooldown(1, 16)
