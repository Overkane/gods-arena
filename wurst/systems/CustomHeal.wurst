package CustomHeal
import FText
import Abilities
import AttachmentPoints
import CustomStatSystem
import CoolNumbers
import TooltipFactory

public function neutralHeal(unit target, real amount)
    null.healTarget(target, amount)

public function unit.healTarget(unit target, real amount)
    this.healTarget(target, amount, true)

public function unit.healTarget(unit target, real amount, boolean showEffect)
    var realAmount = amount

    // Healing enhanced by stats only if there is known heal caster
    if this != null
        if this.hasStat(CustomUnitStats.SPELL_POWER)
            let spellpower = this.getStat(CustomUnitStats.SPELL_POWER)
            realAmount = realAmount * (1 + spellpower)

        if this.hasStat(CustomUnitStats.SPELL_CRIT_CHANCE) and this.hasStat(CustomUnitStats.SPELL_CRIT_POWER)
            let spellCritChance = this.getStat(CustomUnitStats.SPELL_CRIT_CHANCE)
          
            if spellCritChance > GetRandomReal(0, 1)
                realAmount = realAmount * (1 + this.getStat(CustomUnitStats.SPELL_CRIT_POWER))

    let missingHp = target.getMaxHP() - target.getHP()
    realAmount = missingHp > realAmount ? realAmount : missingHp

    if realAmount > 0
        target.addHP(realAmount)
        if showEffect
            target.addEffect(Abilities.healTarget, AttachmentPoints.chest)
        
        coolTextTag(this, target, realAmount.toInt(), HEALING_COLOR, 7)
