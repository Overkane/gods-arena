package Wave
import public ClosureTimers
import public LinkedList
import PlayerData
import Creep
import CreepObjEditing
import ObjectIdGenerator
import SoundUtils
import Abilities
import Funcs
import GodOfNature
import GodOfWater

public constant waveExpList = new LinkedList<int>
..add(200)..add(250)..add(300)..add(350)..add(400)..add(450)..add(500)..add(550)
..add(600)..add(650)..add(700)..add(750)..add(800)..add(850)..add(900)..add(950)
..add(1000)..add(1050)..add(1100)..add(1150)..add(1200)..add(1250)..add(1300)..add(1350)
..add(1400)..add(1450)..add(1500)..add(1550)..add(1600)..add(1650)..add(1700)..add(1800)

public constant waveGoldList = new LinkedList<int>
..add(150)..add(150)..add(150)..add(150)..add(150)..add(150)..add(150)..add(300)
..add(200)..add(200)..add(200)..add(200)..add(200)..add(200)..add(200)..add(400)
..add(250)..add(250)..add(250)..add(250)..add(250)..add(250)..add(250)..add(500)
..add(300)..add(300)..add(300)..add(300)..add(300)..add(300)..add(300)..add(600)

constant AFTER_WAVE_RESTORATION_SOUND = new SoundDefinition(Sounds.restorationPotion)

interface NextStepListener
  abstract function execute()

interface OnWaveStart
  function onStart(Wave wave)

OnWaveStart onStartListener

/** Set listener for when a wave starts */
public function onWaveStart(OnWaveStart onStart)
  onStartListener = onStart

interface OnWaveFinish
  function onFinish(Wave wave)

OnWaveFinish onFinishListener

/** Set listener for when a wave Finishs */
public function onWaveFinish(OnWaveFinish onFinish)
  onFinishListener = onFinish

abstract class WaveStep
  Wave wave

  construct (Wave wave)
    this.wave = wave

  abstract function execute(NextStepListener next)

class WaitStep extends WaveStep
  var duration = 0.

  construct(Wave wave, real duration)
    super(wave)
    this.duration = duration

  override function execute(NextStepListener next)
    doAfter(duration) ->
      next.execute()

class CreepStep extends WaveStep
  let unitId = UNIT_ID_GEN.next()
  CreepDefinition creepDef

  var amount = 1

  construct(Wave wave, int origUnitId)
    super(wave)
    creepDef = new CreepDefinition(unitId, origUnitId)

  override function execute(NextStepListener next)
    doPeriodicallyCounted(0.05, amount) cb ->
      new Creep(unitId, wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg())
      if cb.isLast()
        next.execute()

class BossStep extends WaveStep
  static var bossNumber = 0

  construct(Wave wave)
    super(wave)

  override function execute(NextStepListener next)
    bossNumber++
    if bossNumber == 1
      new GodOfNature(wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg())
    else if bossNumber == 2
      new GodOfWater(wave.spawnPoint.execute().toVec3(), GetRandomReal(0, 360).fromDeg())
    next.execute()

public class WaveBuilder
  let result = new Wave()
  let steps = new LinkedList<WaveStep>
  var currentUnitId = '0000'
  WaveStep currentStep = null
  CreepStep creepStep = null
  int creepAmount = 0
  var bossWave = false

  function wait(real duration)
    if currentStep != null
      steps.add(currentStep)
      currentStep = null

      steps.add(new WaitStep(result, duration))

  function creep(int origUnitId)
    if currentStep != null
      steps.add(currentStep)
      currentStep = null

    creepStep = new CreepStep(result, origUnitId)
    currentStep = creepStep

  function boss()
    currentStep = new BossStep(result)
    bossWave = true
    creepAmount = 1

  function abilities(string abilities)
    creepStep.creepDef.setNormalAbilities(abilities)

  function amount(int amount)
    creepStep.amount = amount
    creepAmount += amount

  function hp(int hp)
    creepStep.creepDef.setHitPointsMaximumBase(hp)

  function hpReg(real hpReg)
    creepStep.creepDef.setHitPointsRegenerationRate(hpReg)

  function mana(int mana)
    creepStep.creepDef.setManaMaximum(mana)
    creepStep.creepDef.setManaInitialAmount(mana)

  function damage(int dmg)
    creepStep.creepDef.setAttack1DamageBase(dmg)

  function attackrate(real rate)
    creepStep.creepDef.setAttack1CooldownTime(rate)

  function armor(int armor)
    creepStep.creepDef.setDefenseBase(armor)

  function armorType(ArmorType armorType)
    creepStep.creepDef.setArmorType(armorType)

  function model(string path)
    creepStep.creepDef.setModelFile(path)

  function scale(real scale)
    creepStep.creepDef.setScalingValue(scale)

  function movespeed(int speed)
    creepStep.creepDef.setSpeedBase(speed)

  function rgb(int red, int green, int blue)
    creepStep.creepDef.setTintingColorRed(red)
    creepStep.creepDef.setTintingColorGreen(green)
    creepStep.creepDef.setTintingColorBlue(blue)

  function goldBounty(int bounty)
    this.result.goldBounty = bounty

  function expBounty(int bounty)
    this.result.expBounty = bounty

  function build() returns Wave
    if currentStep != null
      steps.add(currentStep)
      currentStep = null
    result.steps = steps
    result.creepAmount = creepAmount
    result.bossWave = bossWave
    return result

constant NEXT_WAVE = new SoundDefinition(Sounds.questNew)
constant WAVE_FINISHED = new SoundDefinition(Sounds.questCompleted)

public class Wave
  LinkedList<WaveStep> steps
  Vec2Function spawnPoint
  Vec2Function revivePoint

  var goldBounty = 0
  var expBounty = 0

  var creepAmount = 0

  var bossWave = false

  var isDone = false

  function spawn()
    onStartListener.onStart(this)
    // Clears game statistic for wave
    for p in pData
      pData.get(p)
      ..clearPhysDamage()
      ..clearSpellDamage()
      ..clearDamageTaken()
    printTimed("|cffceac15>> The wave begins!", 10.)
    NEXT_WAVE.play()
    nextStep()

  function nextStep()
    if steps.isEmpty()
      isDone = true
    else
      steps.dequeue().execute() ->
        nextStep()

  function finish()
    onFinishListener.onFinish(this)
    printTimed("|cff1f9913>> Wave cleared!", 10.)
    WAVE_FINISHED.play()

    pData.forEach() (player p, PlayerData playerData) ->
      if playerData.getHero().have
        let heroUnit = playerData.getHero().hero.actor

        if not heroUnit.isAlive()
          heroUnit.revive(revivePoint.execute(), true)
          heroUnit.setHP(heroUnit.getMaxHP() * 0.35)
          heroUnit.setMana(heroUnit.getMaxMana() * 0.15)
          p.select(heroUnit)
          p.setCameraPosition(heroUnit.getPos())
        else
          AFTER_WAVE_RESTORATION_SOUND.play(1, 30)
          flashEffect(Abilities.replenishHealthCasterOverhead, heroUnit.getPos())
          heroUnit.addHP(heroUnit.getMaxHP() * 0.45)
          heroUnit.addMana(heroUnit.getMaxMana() * 0.3)
