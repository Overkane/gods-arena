package Boss
import PhysicsEntity
import GameConstants
import CurrentArena

interface BossIsDead
  abstract function execute()

public BossIsDead onBossDefeat

public constant bossList = new LinkedList<unit>

public function unit.isBoss() returns bool
  return bossList.has(this)

public class Boss extends UnitEntity
  use PhysicsModule
  
  construct(int typId, vec3 pos, angle ang)
    super(createUnit(ENEMY_PLAYER, typId, pos.toVec2(), ang))
    actor.setAnimation("birth")

    bossList.add(actor)

    EventListener.add(actor, EVENT_PLAYER_UNIT_DEATH) () ->
      onBossDefeat.execute()
      bossList.remove(actor)

  override function update()
    physicsUpdate(this)
    super.update()

  override function addVel(vec3 vel)
    super.addVel(vel * 0.2)

  override function onEscapeGround()
    actor.setPropWindow(0 .fromDeg())

  override function onGroundHit()
    scaleVel(0.75)
    actor.setPropWindow(actor.getDefaultPropWindow())

  override function setXY(vec3 tpos)
    pos = tpos
    if tpos.x > currentArenaRect.getMaxX() or tpos.x < currentArenaRect.getMinX()
      vel = vec3(-vel.x, vel.y, vel.z)
      pos += vel
    else if tpos.y > currentArenaRect.getMaxY() or tpos.y < currentArenaRect.getMinY()
      vel = vec3(vel.x, -vel.y, vel.z)
      pos += vel
      
    actor.setXYZReal(pos)