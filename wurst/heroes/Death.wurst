package Death
import ClosureEvents
import ClosureTimers
import Reference
import SaveLoadSystem
import PlayerData
import GameConstants
import GameStatsUI

var ended = false

init
  EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
    let tunit = EventData.getTriggerUnit()
    if tunit.isType(UNIT_TYPE_HERO) and tunit.getOwner().isEnemyOf(ENEMY_PLAYER) and not ended
      let aliveCount = new Reference(0)
      pData.forEach() (player key, PlayerData value) ->
        if value.getHero().have and value.getHero().hero.actor.isAlive()
          aliveCount.val += 1
      if aliveCount.into() == 0
        print(">> All Heroes died. You lost!".color(COLOR_GOLD_STR))
        print(">> Game will end momentarily.".color(COLOR_GOLD_STR))
        savePlayerData()
        gameStatsUIFrame.show()
        ended = true
        doAfter(90.) ->
          EndGame(true)
      else
        printTimedToPlayer(COLOR_GOLD_STR + ">>|r You died. You will revive at the end of the round.", 15., tunit.getOwner())
