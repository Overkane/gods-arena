package MeteorSpell
import Assets
import ClosureTimers
import ClosureForGroups
import AbilityTooltipGenerator
import GameConstants
import Entity
import DamageSystem
import PhysicsProjectile
import Heightmap
import AbilityDefinitionExtension

public constant METEOR_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant RealLevelClosure SPELL_DMG = lvl -> 30. + 125. * lvl
constant RealLevelClosure SPELL_AOE = lvl -> 250. + 25 * lvl

init
    EventListener.onPointCast(METEOR_ABIL_ID) (caster, spellTarget) ->
        let lvl = caster.getAbilityLevel(METEOR_ABIL_ID)
        let aoe = SPELL_AOE.run(lvl)
        flashEffect(Units.infernalBirth, spellTarget)
        doAfter(0.9) ->
            for i = 0 to 9
                new Shrapnel(spellTarget.withHeightMap(), Abilities.redDragonMissile)

            forUnitsInRange(spellTarget, aoe) (target) ->
                let eff = target.addEffect(Abilities.flameStrikeEmbers, AttachmentPoints.origin)
                doAfter(0.9) ->
                    eff.destr()
                if target.isEnemyOf(caster.getOwner()) and target.isAlive()
                    if not target.isStrong() and target.getEntity() != null // TODO God of Water summons aren't entities
                        let knockBackDistance = 1 - target.getPos().distanceTo(spellTarget) / aoe
                        target.getEntity().addVel(ZERO3.polarProject(800 * ANIMATION_PERIOD * knockBackDistance, spellTarget.angleTo(target.getPos()), (27).fromDeg()))
                    caster.dealMagicalDamage(target, SPELL_DMG.run(lvl))
                    flashEffect(Abilities.incinerateBuff, target, "origin")


@compiletime
function genAbility()
    new ChannelAbilityPreset(METEOR_ABIL_ID, NORMAL_SPELL_LEVELS, true, new AbilityTooltipGenerator("A chunky meteor from the sky which crushes your enmies in a big area with over 9000Â° Celsius."))
    ..makeQAbility()
    ..presetIcon(Icons.bTNInfernal)
    ..presetOption(Option.TARGETIMAGE, true)

    ..tooltipStartListen()
    ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
    ..presetTargetTypes(Targettype.POINT)
    ..setName("Chaos Meteor")
    ..addTooltipProperty("Damage", SPELL_DMG)
    ..presetManaCost(lvl -> 155 + 55 * (lvl-1))
    ..presetAreaofEffect(SPELL_AOE)
    ..presetCooldown(lvl -> 28. - lvl)
    ..tooltipStopListen()
