package ElementalMissiles
import AbilityTooltipGenerator
import GameConstants
import Abilities
import DamageSystem
import MapIcons
import MapModels
import OrderIds
import StunAbilityPreset
import BuffObjEditing
import BuffIds
import TooltipFactory
import ClosureForGroups
import DummyStatCaster
import AbilityDefinitionExtension

public constant ELEMENTAL_MISSILE_ID = compiletime(ABIL_ID_GEN.next())
constant ELEMENTAL_DISCHARGE_ID = compiletime(BUFF_ID_GEN.next())
constant FROSTNOVA_ID = compiletime(ABIL_ID_GEN.next())
constant LIGHTNING_STUN_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure INT_AS_DAMAGE = lvl -> 0.5 * lvl
constant IntLevelClosure ELEMENTAL_REACTION_CHANCE = lvl -> 15 + 15 * lvl

constant NUMBER_OF_SPELLS = 3

tuple interval(int min, int max)

init
  DamageEvent.addUnreducedListener(DamageEvents.START castTo int) -> 
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if tunit.hasAbility(ELEMENTAL_DISCHARGE_ID) and sunit.hasAbility(ELEMENTAL_MISSILE_ID)
      and DamageEvent.getType() == DamageType.ATTACK

      let lvl = sunit.getAbilityLevel(ELEMENTAL_MISSILE_ID)
      let intMultiplier = INT_AS_DAMAGE.run(lvl)
      sunit.addOnetimeStat(CustomUnitStats.BONUS_ATTACK_DAMAGE, sunit.getInt(true) * intMultiplier)

  // Effects work only if target couldn't evade
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if tunit.hasAbility(ELEMENTAL_DISCHARGE_ID) and sunit.hasAbility(ELEMENTAL_MISSILE_ID)
      and DamageEvent.getType() == DamageType.ATTACK

      let lvl = sunit.getAbilityLevel(ELEMENTAL_MISSILE_ID)
      let spellOverallChance = ELEMENTAL_REACTION_CHANCE.run(lvl)
      let chancePerSpell = (spellOverallChance / NUMBER_OF_SPELLS).toInt()
      let fireInterval = interval(1, chancePerSpell)
      let iceInterval = interval(fireInterval.max + 1, fireInterval.max + chancePerSpell)
      let lightningInterval = interval(iceInterval.max + 1, iceInterval.max + chancePerSpell)
      
      let spellNumber = GetRandomInt(1, 100)

      // Fire
      if spellNumber.isBetween(fireInterval.min, fireInterval.max)
        flashEffect(Abilities.fireLordDeathExplode, tunit.getPos())
        forUnitsInRange(tunit.getPos(), 200) (unit u) ->
          if u.isAlive() and u.isEnemyOf(sunit.getOwner())
            sunit.dealMagicalDamage(u, sunit.getInt(true) * 3.)
      
      // Ice
      else if spellNumber.isBetween(iceInterval.min, iceInterval.max)
        flashEffect(Abilities.frostNovaTarget, tunit.getPos())
        new DummyStatCaster()
        ..origin(sunit.getPos())
        ..caster(sunit)
        ..castTarget(FROSTNOVA_ID, 1, OrderIds.frostnova, tunit)

        forUnitsInRange(tunit.getPos(), 200) (unit u) ->
          if u.isAlive() and u.isEnemyOf(sunit.getOwner())
            sunit.dealMagicalDamage(u, sunit.getInt(true) * 1.)

      // Lightning
      else if spellNumber.isBetween(lightningInterval.min, lightningInterval.max)
        flashEffect(Abilities.lightningBoltMissile, tunit.getPos())
        new DummyStatCaster()
        ..delay(1.)
        ..caster(sunit)
        ..castTarget(LIGHTNING_STUN_ID, 1, OrderIds.thunderbolt, tunit)
        
        sunit.dealMagicalDamage(tunit, sunit.getInt(true) * 6.)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("The Elementor enhances his attack with elemental missiles, adding bonus damage based on the hero's intelligence.\n" +
    "The impact of elemental missiles may produce random elemental reactions.")
  new AbilityDefinitionDarkRangerBlackArrow(ELEMENTAL_MISSILE_ID)
  ..registerTooltipGenerator(tooltip)
  ..makeDAbility()
  ..setIconNormal(MapIcons.aTCBeamShower)
  ..setIconTurnOff(MapIcons.aTCBeamShower)
  ..presetTargetsAllowed(lvl -> TargetsAllowed.enemies)
  ..setMissileArt(MapModels.missilePurple)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..setLevels(INNATE_SPELL_LEVELS)
  ..setName("Elemental Missile")
  ..addTooltipProperty("Int as Damage", lvl -> (INT_AS_DAMAGE.run(lvl) * 100).percRound())
  ..addTooltipProperty("Elemental Reaction Chance", lvl -> ELEMENTAL_REACTION_CHANCE.run(lvl).toString() + "%")
  ..presetManaCost(lvl -> 15 + 20 * (lvl-1))
  ..tooltipStopListen()

  ..presetBuffs(lvl -> ELEMENTAL_DISCHARGE_ID.toRawCode())
  ..presetNumberofSummonedUnits(lvl -> 0)
  ..presetSummonedUnitType(lvl -> "")
  ..presetDamageBonus(lvl -> 0)
  ..presetDurationNormal(lvl -> 0.01)
  ..presetDurationHero(lvl -> 0.01)


  new BuffDefinition(ELEMENTAL_DISCHARGE_ID, BuffIds.slow)
  ..setIcon(MapIcons.aTCBeamShower)
  ..setTooltipNormal(1, "Elemental Discharge")
  ..setArtTarget("")


  new AbilityDefinitionLichFrostNova(FROSTNOVA_ID)
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setDummyAbility()
  ..setArtTarget("")
  ..presetAreaofEffectDamage(lvl -> 0)
  ..presetSpecificTargetDamage(lvl -> 0)
  ..presetAreaofEffect(lvl -> 200)
  ..presetDurationNormal(lvl -> 1)
  ..presetDurationHero(lvl -> getHeroStunDuration(1))
  

  new StunAbilityPreset(LIGHTNING_STUN_ID, NORMAL_SPELL_LEVELS)
  ..presetDurationNormal(lvl -> 0.5)
  ..presetDurationHero(lvl -> getHeroStunDuration(0.5))