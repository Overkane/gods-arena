package TimeSiphon
import AbilityTooltipGenerator
import GameConstants
import DamageSystem
import AbilityDefinitionExtension

public constant SIPHON_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant DUMMY_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant RealLevelClosure SPELL_CAST_RANGE = lvl -> 700. + 85 * lvl
constant SPELL_CD = 17.
constant DURATION = 7.

@compiletime
function genAbil()
    new AbilityDefinitionDarkRangerDrain(SIPHON_ABIL_ID)
    ..registerTooltipGenerator(new AbilityTooltipGenerator(Targettype.UNIT, "The Elementor siphons energy from the target to himself, replenishing mana and hitpoints."))
    ..makeEAbility()
    ..presetIcon(Icons.bTNDrain)
    
    ..tooltipStartListen()
    ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
    ..setLevels(NORMAL_SPELL_LEVELS)
    ..setName("Time Siphon")
    ..presetManaPointsDrained(lvl -> 25. * lvl)
    ..presetHitPointsDrained(lvl -> 30. * lvl)
    ..addTooltipProperty("Duration", lvl -> DURATION)
    ..presetCastRange(SPELL_CAST_RANGE)
    ..presetManaCost(lvl -> 40 + 30 * lvl)
    ..presetCooldown(lvl -> SPELL_CD - lvl)
    ..tooltipStopListen()

    ..presetDurationNormal(lvl -> DURATION)
    ..presetDurationHero(lvl -> getHeroStunDuration(DURATION))
    ..presetTargetsAllowed(lvl -> commaList(TargetsAllowed.enemies))
