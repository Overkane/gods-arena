package StormHammer
import Icons
import AbilityTooltipGenerator
import StormHammerBuff
import Buff
import OrderIds
import ClosureEvents
import HeroSpellConstants
import DummyStatCaster

public constant STORM_HAMMER_ID = compiletime(ABIL_ID_GEN.next())
public constant STORM_HAMMER_CHAIN_LIGHTNING_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure CHAIN_LIGHTNING_DAMAGE = lvl -> 25. + 15. * (lvl-1)
IntLevelClosure CHAIN_LIGHTNING_NUMBER_OF_TARGETS = lvl -> 3 + lvl
RealLevelClosure CHARGE_DURATION = lvl -> 4.5 + 0.5 * lvl

init 
  EventListener.onCast(STORM_HAMMER_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(STORM_HAMMER_ID)
    let spellDuration = CHARGE_DURATION.run(lvl)

    new StormHammerBuff(spellDuration).apply(caster)

  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.getBuff(StormHammerBuff.typeId) != null
      let lvl = sunit.getAbilityLevel(STORM_HAMMER_ID)
      let tunit = DamageEvent.getTarget()

      new DummyStatCaster()
      ..origin(sunit.getPos())
      ..caster(sunit)
      ..castTarget(STORM_HAMMER_CHAIN_LIGHTNING_ID, lvl, OrderIds.chainlightning, tunit)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Hero charges his hammer for a short duration. While hammer charged, each hero attack calls chain lightning.")
  new ChannelAbilityPreset(STORM_HAMMER_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..setLevelSkipRequirement(0)
  ..presetIcon(Icons.bTNStormHammer)
  ..presetButtonPosNormal(0, 2)
  ..presetButtonPosResearch(0, 0)
  ..presetHotkey("Q")

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.NONE)
  ..setName("Storm Hammer")
  ..addTooltipProperty("Chain Lightning Damage", CHAIN_LIGHTNING_DAMAGE)
  ..addTooltipProperty("Chain Lightning Number of Targets", CHAIN_LIGHTNING_NUMBER_OF_TARGETS)
  ..addTooltipProperty("Charge Duration", CHARGE_DURATION)
  ..presetManaCost(lvl -> 90 + 10 * lvl)
  ..presetCooldown(lvl -> 20. - lvl)
  ..tooltipStopListen()

  new AbilityDefinitionFarseerChainLightning(STORM_HAMMER_CHAIN_LIGHTNING_ID)
  ..setDummyAbility()
  ..presetDamageperTarget(CHAIN_LIGHTNING_DAMAGE)
  ..presetNumberofTargetsHit(CHAIN_LIGHTNING_NUMBER_OF_TARGETS)
  ..presetDamageReductionperTarget(lvl -> 0)
    