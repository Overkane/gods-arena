package StormHammer
import AbilityTooltipGenerator
import StormHammerBuff
import Buff
import OrderIds
import ClosureEvents
import GameConstants
import DummyStatCaster
import DamageSystem
import AbilityDefinitionExtension

public constant STORM_HAMMER_ID = compiletime(ABIL_ID_GEN.next())
public constant STORM_HAMMER_CHAIN_LIGHTNING_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure CHAIN_LIGHTNING_DAMAGE = lvl -> 30. + 25. * (lvl-1)
IntLevelClosure CHAIN_LIGHTNING_NUMBER_OF_TARGETS = lvl -> 3 + (lvl / 2).floor()
RealLevelClosure CHARGE_DURATION = lvl -> 4.5 + 0.5 * lvl


init 
  EventListener.onCast(STORM_HAMMER_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(STORM_HAMMER_ID)
    let spellDuration = CHARGE_DURATION.run(lvl)

    new StormHammerBuff(spellDuration).apply(caster)

  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.getBuff(StormHammerBuff.typeId) != null
      let lvl = sunit.getAbilityLevel(STORM_HAMMER_ID)
      let tunit = DamageEvent.getTarget()

      new DummyStatCaster()
      ..origin(sunit.getPos())
      ..caster(sunit)
      ..castTarget(STORM_HAMMER_CHAIN_LIGHTNING_ID, lvl, OrderIds.chainlightning, tunit)


@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Charges hammer for a short duration. While hammer is charged, each normal attack calls chain lightning.")
  new ChannelAbilityPreset(STORM_HAMMER_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..makeQAbility()
  ..presetIcon(Icons.bTNStormHammer)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..presetTargetTypes(Targettype.NONE)
  ..setName("Storm Hammer")
  ..addTooltipProperty("Chain Lightning Damage", CHAIN_LIGHTNING_DAMAGE)
  ..addTooltipProperty("Chain Lightning Number of Targets", CHAIN_LIGHTNING_NUMBER_OF_TARGETS)
  ..addTooltipProperty("Charge Duration", CHARGE_DURATION)
  ..presetManaCost(lvl -> 90 + 25 * lvl)
  ..presetCooldown(lvl -> 20. - lvl * 2)
  ..tooltipStopListen()

  new AbilityDefinitionFarseerChainLightning(STORM_HAMMER_CHAIN_LIGHTNING_ID)
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setDummyAbility()
  ..presetDamageperTarget(CHAIN_LIGHTNING_DAMAGE)
  ..presetNumberofTargetsHit(CHAIN_LIGHTNING_NUMBER_OF_TARGETS)
  ..presetDamageReductionperTarget(lvl -> 0)
    