package Tempest
import Icons
import AbilityTooltipGenerator
import HeroSpellConstants

public constant TEMPEST_ID = compiletime(ABIL_ID_GEN.next())

RealLevelClosure DAMAGE = lvl -> 60. + 50. * (lvl-1)
RealLevelClosure DAMAGE_INTERVAL = lvl -> 1
RealLevelClosure AOE = lvl -> 350. + 50. * (lvl-1)
RealLevelClosure DURATION = lvl -> 10.

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Calls down a storm of lightning to strike enemy units.")
  new AbilityDefinitionMonsoon(TEMPEST_ID)
  ..registerTooltipGenerator(tooltip)
  ..presetIcon(Icons.bTNPurge)
  ..presetButtonPosNormal(3, 2)
  ..presetButtonPosResearch(3, 0)
  ..presetHotkey("R")
  ..setRequiredLevel(ULTIMATE_LEVEL_REQ)
  ..setLevelSkipRequirement(ULTIMATE_LEVEL_SKIP_REQ)
  
  ..tooltipStartListen()
  ..setLevels(ULTIMATE_SPELL_LEVELS)
  ..setName("Tempest")
  ..addTooltipProperty("Damage per lightning", DAMAGE)
  ..presetDamageInterval(DAMAGE_INTERVAL)
  ..presetAreaofEffect(AOE)
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 150 + 25 * lvl)
  ..presetCooldown(lvl -> 75)
  ..tooltipStopListen()
  
  ..presetDamageDealt(DAMAGE)
  ..presetDurationHero(DURATION)