package ProtectiveShellSpell
import ClosureEvents
import Assets
import ClosureForGroups
import AbilityTooltipGenerator
import InstantDummyCaster
import Orders
import ClosureTimers
import BonusHandler
import GameConstants
import CreepAggro

public constant SHELL_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant DUMMY_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant RealLevelClosure SPELL_REGEN = lvl -> 3. + 6. * lvl
constant RealLevelClosure SPELL_ARMOR = lvl -> 7. + 3. * lvl
// constant RealLevelClosure REDUCE_DMG = lvl -> 0. * lvl
constant SPELL_AOE = 320.
constant SPELL_DURATION = 7.
constant FLASH_EFFECT_DELAY = 0.9

init
    EventListener.onCast(SHELL_ABIL_ID) caster ->
        let lvl = caster.getAbilityLevel(SHELL_ABIL_ID)
        let owner = caster.getOwner()

        let shellEffect = caster.addEffect(Doodads.ruins_Shells4, AttachmentPoints.origin)
        ..setScale(5.)

        let regen = SPELL_REGEN.run(lvl)
        let armor = SPELL_ARMOR.run(lvl)
        caster..setPropWindow(0 .fromDeg())
        ..addBonus(Bonus.LIFEREGEN, regen)
        ..addBonus(Bonus.ARMOR, armor)

        forUnitsInRange(caster.getPos(), SPELL_AOE) (target) ->
            if target.isEnemyOf(owner)
                InstantDummyCaster.castTarget(owner, DUMMY_ABIL_ID, lvl, OrderIds.cripple, target)
                target.clearAggro()
                caster.addAggro(target, 200 + lvl * 100)

        doAfter(SPELL_DURATION - 1.) ->    
            shellEffect.destr()
            
        doAfter(SPELL_DURATION) ->
            caster..setPropWindow(caster.getDefaultPropWindow())
            ..addBonus(Bonus.LIFEREGEN, -regen)
            ..addBonus(Bonus.ARMOR, -armor)



@compiletime
function genAbility()
    new ChannelAbilityPreset(SHELL_ABIL_ID, NORMAL_SPELL_LEVELS, true, new AbilityTooltipGenerator("The Crab has enough of this shit and hides in its pocket shell. Increases defense but |cffb93939taunts|r nearby enemies."))
    ..presetButtonPosNormal(0, 2)
    ..presetButtonPosResearch(0, 0)
    ..presetIcon(Icons.bTNShimmerWeed)

    ..tooltipStartListen()
    ..presetTargetTypes(Targettype.NONE)
    ..setName("Protective Shell")
    ..presetHotkey("Q")
    ..addTooltipProperty("Life Regen", SPELL_REGEN)
    ..addTooltipProperty("Armor", SPELL_ARMOR)
    ..presetManaCost(lvl -> 50 + lvl * 10)
    ..presetCooldown(lvl -> 16. - lvl)

    ..tooltipStopListen()

    new AbilityDefinitionCripple(DUMMY_ABIL_ID)
    ..setDummyAbility()
    ..setLevels(NORMAL_SPELL_LEVELS)
    ..presetAreaofEffect(lvl -> 1)
    ..presetMovementSpeedReduction(lvl -> 0)
    ..presetDamageReduction(lvl -> 0.2)
    ..presetDurationNormal(lvl -> SPELL_DURATION)
    ..presetDurationHero(lvl -> SPELL_DURATION)
    ..presetAttackSpeedReduction(lvl -> 0)
