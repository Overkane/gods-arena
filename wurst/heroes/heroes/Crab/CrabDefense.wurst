package CrabDefense
import AbilityTooltipGenerator
import GameConstants
import PassiveAbilityPreset
import Orders
import TooltipFactory
import AbilityDefinitionExtension
import Crabling
import UpgradeObjEditing
import ProtectiveShellBuff
import Buff

public constant CRAB_DEFENSE_ID = compiletime(ABIL_ID_GEN.next())
public constant CRAB_DEFENSE_UPGRADE_ID = compiletime(UPGD_ID_GEN.next())

constant RealLevelClosure SPAWN_CHANCE = lvl -> 0.1 + 0.05 * lvl
public constant RealLevelClosure STRENGTH_DAMAGE = lvl -> 0.3 * lvl
public constant RealLevelClosure DURATION = lvl -> 8.

init
  DamageEvent.addListener() -> 
    let target = DamageEvent.getTarget()
    let source = DamageEvent.getSource()

    if target.hasAbility(CRAB_DEFENSE_ID) and source.isEnemyOf(target)
      let lvl = target.getAbilityLevel(CRAB_DEFENSE_ID)
      var chance = SPAWN_CHANCE.run(lvl)
      let rand = GetRandomReal(0, 1)

      if target.hasBuff(ProtectiveShellBuff.typeId)
        let protectiveShellBuff = target.getBuff(ProtectiveShellBuff.typeId) castTo ProtectiveShellBuff
        chance += protectiveShellBuff.bonusCrablingSpawnChance

      if rand < chance
        new Crabling(target, target.getPos().toVec3(), target.getPos().angleTo(target.getPos()))
        .actor
        ..issueTargetOrderById(OrderIds.attack, source)

@compiletime function gen()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Has a chance to spawn a defensive |cffce951acrabling|r when attacked. Number of total |cffce951acrablings|r is shown as armor upgrade")
  new PassiveAbilityPreset(CRAB_DEFENSE_ID, INNATE_SPELL_LEVELS, tooltip)
  ..makeDAbility()
  ..presetIcon(Icons.pASBTNBerserk)
  ..registerTooltipGenerator(tooltip)

  ..tooltipStartListen()
  ..setName("Crab Defense")
  ..addTooltipProperty("Chance to spawn", lvl -> (SPAWN_CHANCE.run(lvl) * 100).percRound())
  ..addTooltipProperty("Crab Str Damage Multiplier", lvl -> (STRENGTH_DAMAGE.run(lvl) * 100).percRound())
  ..addTooltipProperty("Duration", lvl -> DURATION.run(lvl).round())
  ..tooltipStopListen()

  new UpgradeDefinition(CRAB_DEFENSE_UPGRADE_ID)
  ..setLevels(999)
  ..setClass(UpgradeClass.Armor)