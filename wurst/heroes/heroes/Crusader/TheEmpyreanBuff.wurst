package TheEmpyreanBuff
import public PresetBuffs
import Assets
import BonusHandler
import SoundUtils
import TooltipFactory

constant buffTuple = compiletime(createDummyBuffObject("The Empyrean".color(POSITIVE_COLOR), 
  "Bonus attack damage but slower attackspeed. Hero abilities are improved.",  Icons.bTNCharm))

public class TheEmpyreanBuff extends NormalBuff
  private real bonusDamage
  private real attackSpeedSlow 

  int lvl

  constant ON_APPLY_SOUND = new SoundDefinition(Sounds.divineShield)
  constant ON_END_SOUND = new SoundDefinition(Sounds.paladinDivineShieldDeath1)

  construct(real dur, real bonusDamage, real attackSpeedSlow, int lvl)
    super(dur, buffTuple)
    this.bonusDamage = bonusDamage
    this.attackSpeedSlow = attackSpeedSlow
    this.lvl = lvl
    
  override function onApply()
    ON_APPLY_SOUND.play()
    target..addBonus(Bonus.DAMAGE, bonusDamage)
    ..addBonus(Bonus.ATTACKSPEED, -attackSpeedSlow)
    ..setScale(1.75)

  override function onEnd()
    ON_END_SOUND.play()
    target..addBonus(Bonus.DAMAGE, -bonusDamage)
    ..addBonus(Bonus.ATTACKSPEED, attackSpeedSlow)
    ..setScale(1)