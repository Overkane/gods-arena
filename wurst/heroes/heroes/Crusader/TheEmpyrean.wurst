package TheEmpyrean
import Icons
import AbilityTooltipGenerator
import public TheEmpyreanBuff
import Entity
import GameConstants
import TooltipFactory

public constant THE_EMPYREAN_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure DAMAGE = lvl -> 50. + 50. * (lvl-1)
constant RealLevelClosure ATTACK_SPEED_SLOW = lvl -> 90
constant RealLevelClosure DURATION = lvl -> 30.

public constant RealLevelClosure SACRED_GROUNDS_AOE_MULTIPLIER = lvl -> 1.5
public constant RealLevelClosure DIVINE_SHIELD_MULTIPLIER = lvl -> 2.
public constant RealLevelClosure HOLY_SMITE_PROC_CHANCE = lvl -> 80. + 10. * (lvl-1)

init 
  EventListener.onCast(THE_EMPYREAN_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(THE_EMPYREAN_ID)
    let bonusDamage = DAMAGE.run(lvl)
    let attackSpeedSlow = ATTACK_SPEED_SLOW.run(lvl)
    let spellDuration = DURATION.run(lvl)

    new TheEmpyreanBuff(spellDuration, bonusDamage, attackSpeedSlow, lvl).apply(caster)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Hero enhance himself and all his abilities, but his attack speed becomes slower.")
  new ChannelAbilityPreset(THE_EMPYREAN_ID, ULTIMATE_SPELL_LEVELS, true, tooltip)
  ..setLevelSkipRequirement(ULTIMATE_LEVEL_SKIP_REQ)
  ..setRequiredLevel(ULTIMATE_LEVEL_REQ)
  ..presetIcon(Icons.bTNCharm)
  ..presetButtonPosNormal(3, 2)
  ..presetButtonPosResearch(3, 0)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.NONE)
  ..presetHotkey("R")
  ..setName("The Empyrean")
  ..addTooltipProperty("Additional Damage", DAMAGE)
  ..addTooltipProperty("Attack Speed Slow", ATTACK_SPEED_SLOW)
  ..addTooltipProperty("Sacred Ground AOE Multiplier", SACRED_GROUNDS_AOE_MULTIPLIER)
  ..addTooltipProperty("Divine Shield Damage Reduction Multiplayer", DIVINE_SHIELD_MULTIPLIER)
  ..addTooltipProperty("Holy Smite Proc Chance", lvl -> HOLY_SMITE_PROC_CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 125 + 25 * lvl)
  ..presetCooldown(lvl -> 90)
  ..tooltipStopListen()