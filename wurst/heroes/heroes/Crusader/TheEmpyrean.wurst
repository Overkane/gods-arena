package TheEmpyrean
import AbilityTooltipGenerator
import public TheEmpyreanBuff
import Entity
import GameConstants
import TooltipFactory
import AbilityDefinitionExtension

public constant THE_EMPYREAN_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure DAMAGE = lvl -> 100. * lvl
constant RealLevelClosure ATTACK_SPEED_SLOW = lvl -> 60
constant RealLevelClosure DURATION = lvl -> 30.

public constant RealLevelClosure SACRED_GROUNDS_AOE_MULTIPLIER = lvl -> 1.5
public constant RealLevelClosure DIVINE_SHIELD_MULTIPLIER = lvl -> 2.
public constant RealLevelClosure HOLY_SMITE_PROC_CHANCE = lvl -> 70. + 10. * (lvl-1)

init 
  EventListener.onCast(THE_EMPYREAN_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(THE_EMPYREAN_ID)
    let bonusDamage = DAMAGE.run(lvl)
    let attackSpeedSlow = ATTACK_SPEED_SLOW.run(lvl)
    let spellDuration = DURATION.run(lvl)

    new TheEmpyreanBuff(spellDuration, bonusDamage, attackSpeedSlow, lvl).apply(caster)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Temporarily enhances all abilities and gives bonus attack damage, but reduces attack speed.")
  new ChannelAbilityPreset(THE_EMPYREAN_ID, ULTIMATE_SPELL_LEVELS, true, tooltip)
  ..makeRAbility()
  ..presetIcon(Icons.bTNCharm)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.NONE)
  ..setName("The Empyrean")
  ..addTooltipProperty("Additional Damage", DAMAGE)
  ..addTooltipProperty("Attack Speed Slow", ATTACK_SPEED_SLOW)
  ..addTooltipProperty("Sacred Ground AOE Multiplier", SACRED_GROUNDS_AOE_MULTIPLIER)
  ..addTooltipProperty("Divine Shield Damage Reduction Multiplayer", DIVINE_SHIELD_MULTIPLIER)
  ..addTooltipProperty("Holy Smite Proc Chance", lvl -> HOLY_SMITE_PROC_CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 100 + 50 * lvl)
  ..presetCooldown(lvl -> 75)
  ..tooltipStopListen()