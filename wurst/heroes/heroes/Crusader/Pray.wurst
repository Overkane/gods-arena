package Pray
import AbilityTooltipGenerator
import ClosureEvents
import Buff
import DialogBox
import ClosureTimers
import PlayerData
import PrayBuff
import HashMap
import GameConstants
import AbilityDefinitionExtension

public constant PRAY_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure BONUS_DAMAGE = lvl -> 4. * lvl
constant RealLevelClosure BONUS_AS = lvl -> 3. * lvl
constant RealLevelClosure BONUS_ARMOR = lvl -> 1. * lvl
constant RealLevelClosure BONUS_HP_REG = lvl -> 1. * lvl
constant RealLevelClosure BONUS_STATS = lvl -> 1. * lvl
public constant PRAY_AOE = 600.

DialogBox prayDialog

public enum PrayBuffTypes
  DAMAGE
  ARMOR
  ATTACK_SPEED
  HP_REG
  STATS

public constant prayBuffMap = new IterableMap<PrayBuffTypes, real>
..put(PrayBuffTypes.DAMAGE, 0)
..put(PrayBuffTypes.ARMOR, 0)
..put(PrayBuffTypes.ATTACK_SPEED, 0)
..put(PrayBuffTypes.HP_REG, 0)
..put(PrayBuffTypes.STATS, 0)

init 
  nullTimer() -> 
    prayDialog = new DialogBox("Choose God to Pray")
    
    prayDialog.addButton("God of Fire (+4 Damage * ability level)") -> 
      updateBuff(PrayBuffTypes.DAMAGE)
    
    prayDialog.addButton("God of Wind (+3 Attack Speed * ability level)") ->
      updateBuff(PrayBuffTypes.ATTACK_SPEED)
      
    prayDialog.addButton("God of Earth (+1 Armor * ability level)") ->
      updateBuff(PrayBuffTypes.ARMOR)

    prayDialog.addButton("God of Nature (+1 hp reg * ability level)") -> 
      updateBuff(PrayBuffTypes.HP_REG)

    prayDialog.addButton("God of Water (+1 All Stats * ability level)") -> 
      updateBuff(PrayBuffTypes.STATS)

  EventListener.onCast(PRAY_ID) (unit caster) ->
    prayDialog.display(caster.getOwner(), true)

function updateBuff(PrayBuffTypes prayBuffType)
  let p = GetTriggerPlayer()
  pData.get(p).getHero().ifPresent() (h) ->
    if h.actor.hasBuff(PrayMainBuff.typeId)
      h.actor.removeBuffById(PrayMainBuff.typeId)
    
    // After buff main buff is destroyed we are waiting for minor buff to expire to be sure onEnd()
    // it have used old buff values.
    doAfter(BUFF_ANIM_PERIOD) ->
      let lvl = h.actor.getAbilityLevel(PRAY_ID)

      switch prayBuffType
        case PrayBuffTypes.DAMAGE
          prayBuffMap.put(prayBuffType, prayBuffMap.get(prayBuffType) + BONUS_DAMAGE.run(lvl))  
        case PrayBuffTypes.ATTACK_SPEED
          prayBuffMap.put(prayBuffType, prayBuffMap.get(prayBuffType) + BONUS_AS.run(lvl))  
        case PrayBuffTypes.ARMOR
          prayBuffMap.put(prayBuffType, prayBuffMap.get(prayBuffType) + BONUS_ARMOR.run(lvl))  
        case PrayBuffTypes.HP_REG
          prayBuffMap.put(prayBuffType, prayBuffMap.get(prayBuffType) + BONUS_HP_REG.run(lvl))  
        case PrayBuffTypes.STATS
          prayBuffMap.put(prayBuffType, prayBuffMap.get(prayBuffType) + BONUS_STATS.run(lvl))  
      
      new PrayMainBuff().apply(h.actor)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Hero prays to the forgotten Gods and gain a portion of their power, strengthen himself and his allies. Each pray makes the buff stronger.")
  new ChannelAbilityPreset(PRAY_ID, INNATE_SPELL_LEVELS, true, tooltip)
  ..makeDAbility()
  ..presetIcon(Icons.bTNResurrection)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.NONE)
  ..setName("Pray")
  ..presetAreaofEffect(lvl -> PRAY_AOE)
  ..presetCooldown(lvl -> 80.)
  ..tooltipStopListen()

  ..presetManaCost(lvl -> 0)
