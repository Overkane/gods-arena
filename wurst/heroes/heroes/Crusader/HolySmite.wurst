package HolySmite
import Abilities
import ClosureForGroups
import AbilityTooltipGenerator
import StunAbilityPreset
import TheEmpyrean
import GameConstants
import PassiveAbilityPreset
import DummyStatCaster
import Orders
import DamageSystem
import TooltipFactory
import AbilityDefinitionExtension

public constant HOLY_SMITE_ID = compiletime(ABIL_ID_GEN.next())
public constant HOLY_SMITE_STUN_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure CHANCE = lvl -> 13. + 2 * lvl
constant RealLevelClosure DAMAGE = lvl -> 60. * lvl
constant RealLevelClosure BONUS_ATTACK_DAMAGE = lvl -> 0.3 * lvl
constant RealLevelClosure RADIUS = lvl -> 225
constant RealLevelClosure STUN_DURATION = lvl -> 0.3

init 
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.hasAbility(HOLY_SMITE_ID)
      let lvl = sunit.getAbilityLevel(HOLY_SMITE_ID)
      var spellChance = CHANCE.run(lvl)

      if sunit.hasBuff(TheEmpyreanBuff.typeId)
        let empBuff = sunit.getBuff(TheEmpyreanBuff.typeId) castTo TheEmpyreanBuff
        let empLvl = empBuff.lvl
        
        spellChance = HOLY_SMITE_PROC_CHANCE.run(empLvl)

      if spellChance >= GetRandomInt(1, 100)
        let tunit = DamageEvent.getTarget()
        let bonusAttackDamage = BONUS_ATTACK_DAMAGE.run(lvl)
        let spellDamage = DAMAGE.run(lvl) + DamageEvent.getAmount() * bonusAttackDamage
        let spellRadius = RADIUS.run(lvl)

        flashEffect(Abilities.thunderclapCaster, tunit.getPos())

        let dummyCaster = new DummyStatCaster()
        ..delay(1.)
        ..caster(sunit)

        forUnitsInRange(tunit.getPos(), spellRadius) target ->
          if target.isEnemyOf(sunit) and target.isAlive()
            sunit.dealPhysicalDamage(target, spellDamage)

            dummyCaster.castTarget(HOLY_SMITE_STUN_ID, lvl, OrderIds.thunderbolt, target)

            

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Gives a chance on attack to land a heavy strike, "
    + "which deals fixed damage plus part of the hero's attack damage and stuns enemies in small area. "
    + "Bonus damage, based on hero's attack damage, is calculated after resists were applied.".color(IMPORTANT_COLOR))
  new PassiveAbilityPreset(HOLY_SMITE_ID, NORMAL_SPELL_LEVELS, tooltip)
  ..makeEAbility()
  ..presetIcon(Icons.pASBTNFeedBack)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.physical)
  ..setName("Holy Smite")
  ..addTooltipProperty("Chance", lvl -> CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Damage", DAMAGE)
  ..addTooltipProperty("Bonus Attack Damage", lvl -> (BONUS_ATTACK_DAMAGE.run(lvl) * 100).percRound())
  ..addTooltipProperty("Stun Duration", STUN_DURATION)
  ..presetAreaofEffect(RADIUS)
  ..tooltipStopListen()

  new StunAbilityPreset(HOLY_SMITE_STUN_ID, NORMAL_SPELL_LEVELS)
  ..presetDurationNormal(STUN_DURATION)
  ..presetDurationHero(lvl -> getHeroStunDuration(STUN_DURATION.run(lvl)))