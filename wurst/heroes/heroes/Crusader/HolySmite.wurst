package HolySmite
import Icons
import Abilities
import ClosureForGroups
import AbilityTooltipGenerator
import StunAbilityPreset
import TheEmpyreanBuff
import GameConstants
import PassiveAbilityPreset
import DummyStatCaster
import Orders
import DamageSystem
import TooltipFactory

public constant HOLY_SMITE_ID = compiletime(ABIL_ID_GEN.next())
public constant HOLY_SMITE_STUN_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure CHANCE = lvl -> 16. + lvl
constant RealLevelClosure DAMAGE = lvl -> 60. * lvl
constant RealLevelClosure BONUS_STR_DAMAGE = lvl -> 0.5 * lvl
constant RealLevelClosure RADIUS = lvl -> 225
constant RealLevelClosure STUN_DURATION = lvl -> 0.3

init 
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.hasAbility(HOLY_SMITE_ID)
      let lvl = sunit.getAbilityLevel(HOLY_SMITE_ID)

      let bEmpyreanBuff = sunit.getBuff(TheEmpyreanBuff.typeId)
      let hasEmpyreanBuff = bEmpyreanBuff != null

      var spellChance = CHANCE.run(lvl)

      if hasEmpyreanBuff
        let empyreanBuff = bEmpyreanBuff castTo TheEmpyreanBuff
        spellChance = empyreanBuff.holySmiteProcChance

      if spellChance >= GetRandomInt(1, 100)
        let tunit = DamageEvent.getTarget()
        let bonusStrDamage = BONUS_STR_DAMAGE.run(lvl)
        let spellDamage = DAMAGE.run(lvl) + bonusStrDamage * sunit.getStr(true)
        let spellRadius = RADIUS.run(lvl)

        flashEffect(Abilities.thunderclapCaster, tunit.getPos())

        forUnitsInRange(tunit.getPos(), spellRadius) target ->
          if target.isEnemyOf(sunit) and target.isAlive()
            sunit.dealPhysicalDamage(target, spellDamage)

            new DummyStatCaster()
            ..delay(1.)
            ..caster(sunit)
            ..castTarget(HOLY_SMITE_STUN_ID, lvl, OrderIds.thunderbolt, target)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Gives chance on attack to land a heavy strike, which deals fixed damage and stuns enemies in small area.")
  new PassiveAbilityPreset(HOLY_SMITE_ID, NORMAL_SPELL_LEVELS, tooltip)
  ..presetIcon(Icons.pASBTNFeedBack)
  ..presetButtonPosNormal(2, 2)
  ..presetButtonPosResearch(2, 0)
  ..presetHotkey("E")

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.physical)
  ..setName("Holy Smite")
  ..addTooltipProperty("Chance", lvl -> CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Damage", DAMAGE)
  ..addTooltipProperty("Bonus Strength Damage", lvl -> (BONUS_STR_DAMAGE.run(lvl) * 100).percRound())
  ..presetAreaofEffect(RADIUS)
  ..addTooltipProperty("Stun Duration", STUN_DURATION)
  ..tooltipStopListen()

  new StunAbilityPreset(HOLY_SMITE_STUN_ID, NORMAL_SPELL_LEVELS)
  ..presetDurationHero(STUN_DURATION)
  ..presetDurationNormal(STUN_DURATION)