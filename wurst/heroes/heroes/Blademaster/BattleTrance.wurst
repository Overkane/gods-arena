package BattleTrance
import AbilityTooltipGenerator
import GameConstants
import BattleTranceBuff
import ClosureEvents
import AbilityDefinitionExtension
import TooltipFactory

public constant BATTLE_TRANCE_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure DURATION = lvl -> 15.
constant RealLevelClosure BONUS_AS = lvl -> 60. * lvl
constant RealLevelClosure BONUS_MS = lvl -> 30. * lvl
constant RealLevelClosure ATTACK_COOLDOWN_TIME = lvl -> 1.7 - 0.1 * lvl


init 
  EventListener.onCast(BATTLE_TRANCE_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(BATTLE_TRANCE_ID)
    let bonusAS = BONUS_AS.run(lvl)
    let bonusMS = BONUS_MS.run(lvl)
    let attackCooldownTime = ATTACK_COOLDOWN_TIME.run(lvl)
    let duration = DURATION.run(lvl)

    new BattleTranceBuff(duration, bonusAS, bonusMS, attackCooldownTime).apply(caster)
    
    
@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.NONE, "Gives bonus to attack speed, movement speed and decreases base attack cooldown. " +
    "After buff ends hero gets reversed buff for a half of original buff duration.".color(IMPORTANT_COLOR))
  new ChannelAbilityPreset(BATTLE_TRANCE_ID, ULTIMATE_SPELL_LEVELS, true, tooltip)
  ..makeRAbility()
  ..presetIcon(Icons.bTNMirrorImage)

  ..tooltipStartListen()
  ..setName("Battle Trance")
  ..addTooltipProperty("Bonus Attack Speed", BONUS_AS)
  ..addTooltipProperty("Bonus Movespeed", BONUS_MS)
  ..addTooltipProperty("Base Attack Cooldown", lvl -> ATTACK_COOLDOWN_TIME.run(lvl).toString(1))
  ..addTooltipProperty("Duration", DURATION)
  ..presetManaCost(lvl -> 55 + 55 * lvl)
  ..presetCooldown(lvl -> 60)
  ..tooltipStopListen()

  ..presetDurationNormal(DURATION)
  ..presetDurationHero(DURATION)


