package Whirlwind
import AbilityTooltipGenerator
import GameConstants
import PassiveAbilityPreset
import TooltipFactory
import ClosureForGroups
import MapIcons
import UnitAnimations
import DamageSystem
import AbilityDefinitionExtension

public constant WHIRLWIND_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure CHANCE = lvl -> 16. + 1 * lvl
constant RealLevelClosure DAMAGE = lvl -> 0.5 + 0.1 * (lvl-1)
constant RealLevelClosure RADIUS = lvl -> 250.


init 
  DamageEvent.addListener() -> 
    let sunit = DamageEvent.getSource()

    if DamageEvent.getType() == DamageType.ATTACK and sunit.hasAbility(WHIRLWIND_ID)
      let lvl = sunit.getAbilityLevel(WHIRLWIND_ID)

      let spellChance = CHANCE.run(lvl)

      if spellChance >= GetRandomInt(1, 100)
        let tunit = DamageEvent.getTarget()
        let spellDamage = DAMAGE.run(lvl)
        let spellRadius = RADIUS.run(lvl)

        sunit
        ..setAnimation((UnitAnimations.HeroBladeMaster.attackWalkStandSpin.idx))
        ..queueAnimation("stand")

        forUnitsInRange(tunit.getPos(), spellRadius) target ->
          if target.isEnemyOf(sunit) and target.isAlive()
            sunit.dealPhysicalDamage(target, DamageEvent.getAmount() * spellDamage)


@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Adds a chance to perform a whirlwind attack on each normal attack. Whirlwind attack deal damage to all enemies around as a percent of dealt damage by normal attack.")
  new PassiveAbilityPreset(WHIRLWIND_ID, NORMAL_SPELL_LEVELS, tooltip)
  ..makeEAbility()
  ..presetIcon(MapIcons.pASBTNWhirlwind)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.physical)
  ..setName("Whirlwind")
  ..addTooltipProperty("Chance", lvl -> CHANCE.run(lvl).percRound())
  ..addTooltipProperty("Damage", lvl -> (DAMAGE.run(lvl) * 100).percRound())
  ..presetAreaofEffect(RADIUS)
  ..tooltipStopListen()
