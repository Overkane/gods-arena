package PierceShot
import Assets
import Projectile
import AbilityTooltipGenerator
import Tagwalk
import Orders
import BonusHandler
import GameConstants
import Buff
import CustomStatSystem

public constant PIERCESHOT_ID = compiletime(ABIL_ID_GEN.next())

constant TREANT_ID = compiletime(UNIT_ID_GEN.next())

constant RealLevelClosure SPELL_DAMAGE = lvl -> 75. + 85 * lvl

class TreantEntity extends UnitEntity

    construct(unit owner, unit target, int lvl)
        super(createUnit(owner.getOwner(), TREANT_ID, target.getPos(), angle(0))
        ..setTimedLife(15.)
        ..issueTargetOrderById(OrderIds.attack, target)
        ..addBonus(Bonus.DAMAGE, lvl * 30. * (1 + owner.getStat(CustomUnitStats.SPELL_POWER)))
        ..addBonus(Bonus.LIFE, 95. * lvl + (lvl - 1) * 50 * (1 + owner.getStat(CustomUnitStats.SPELL_POWER))))


init
    EventListener.onPointCast(PIERCESHOT_ID) (caster, target) ->
        let projectile = new Projectile(caster.getPos().toVec3(), 86, caster.getOwner(), caster.getPos().angleTo(target), Abilities.ballistaMissile)
        let lvl = caster.getAbilityLevel(PIERCESHOT_ID)
        projectile
            ..setRanged(1200)
            ..setAcc(0.997)
            ..setTarget(target.withTerrainZ(), 50.)

        projectile.onHit() hitTarget ->
            if hitTarget.getOwner().isEnemyOf(caster.getOwner())
                flashEffect(Abilities.ballistaImpact, hitTarget.getPos())
                let dmg = SPELL_DAMAGE.run(lvl) 
                DamageEvent.setNextDamageFromCode()
                caster.damageTarget(hitTarget, SPELL_DAMAGE.run(lvl))
                hitTarget.getEntity().addVel(ZERO3.polarProject(350. + dmg * ANIMATION_PERIOD, projectile.getXYAngle(), 45 .fromDeg()))

                if hitTarget.hasAbility(tagBuffObj.buffId)
                    new TreantEntity(caster, hitTarget, lvl)
                    hitTarget.removeBuffById(TagBuff.typeId)


@compiletime function genAbility()
    let tooltip = new AbilityTooltipGenerator("Shoots an arrow towards the target position, damaging enemies in its path. If hit enemies are currently |cffd803bctagged|r, an ent will be spawned.")
    new ChannelAbilityPreset(PIERCESHOT_ID, NORMAL_SPELL_LEVELS, true, tooltip)
    ..presetButtonPosNormal(0, 2)
    ..presetButtonPosResearch(0, 0)
    ..presetIcon(Icons.bTNGlaiveThrower)

    ..tooltipStartListen()
    ..presetTargetTypes(Targettype.POINT)
    ..presetHotkey("Q")
    ..setName("Piercing Shot")
    ..addTooltipProperty("Damage", SPELL_DAMAGE)
    ..presetManaCost(lvl -> 70 + 10 * lvl)
    ..presetCastRange(lvl -> 1000)
    ..presetCooldown(lvl -> 16. - lvl)
    ..tooltipStopListen()

    new UnitDefinition(TREANT_ID, UnitIds.forceofnature)
    ..setHitPointsMaximumBase(50)
    ..setAttack1DamageBase(15)