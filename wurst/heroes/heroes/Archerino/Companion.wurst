package Companion
import AbilityTooltipGenerator
import Icons
import Tagwalk
import GameConstants
import Entity
import UnitIds
import Abilities
import HashMap
import DamageSystem
import CreepAggro
import SoundUtils

public constant COMPANION_ID = compiletime(ABIL_ID_GEN.next())
constant COMPANION_UNIT_ID = compiletime(UNIT_ID_GEN.next())

constant CANNIBALIZE_ID = compiletime(ABIL_ID_GEN.next())
constant BITE_ID = compiletime(ABIL_ID_GEN.next())
constant BITE_DAMAGE_PER_ATTACK = 5.
constant HIT_SOUNDS = new LinkedList<SoundDefinition>
..add(new SoundDefinition(Sounds.woodLightBashFlesh1))
..add(new SoundDefinition(Sounds.woodLightBashFlesh2))
..add(new SoundDefinition(Sounds.woodLightBashFlesh3))

init
  EventListener.onCast(COMPANION_ID) (unit caster) ->
    new Companion(caster, COMPANION_UNIT_ID, caster.getPos3Zero(), angle(GetRandomDirectionDeg()))

class Companion extends UnitEntity
  static constant playerCompanions = new HashMap<player, unit>

  construct(unit summoner, int typId, vec3 pos, angle ang)
    super(createUnit(summoner.getOwner(), typId, pos.toVec2(), ang))
    flashEffect(Abilities.feralspirittarget, actor.getPos())
    actor
    ..setAnimation("birth")
    ..queueAnimation("stand")

    actor
    ..setMaxHP(actor.getMaxHP().toInt() + summoner.getStr(true) * 15)
    ..setMaxMana(actor.getMaxMana().toInt() + summoner.getInt(true) * 5)
    ..setArmor(actor.getArmor() + summoner.getAgi(true) / 10)
    ..setBaseDamage((summoner.getAgi(true) * 1.5.toInt()), 0)
    ..setHP(actor.getMaxHP())
    ..setMana(actor.getMaxMana())

    if playerCompanions.has(owner)
      let currentCompanion = playerCompanions.get(owner)
      currentCompanion.kill()

    playerCompanions.put(owner, actor)


    EventListener.add(actor, EVENT_PLAYER_UNIT_DEATH) () ->
      playerCompanions.remove(owner)
      terminate()


    EventListener.onTargetCast(actor, BITE_ID) (unit caster, unit target) ->
      HIT_SOUNDS..shuffle().getFirst().play()
      caster.dealPhysicalDamage(target, caster.getBaseDamage(0) * BITE_DAMAGE_PER_ATTACK)
      if target.isAlive()
        new TagBuff(15.)
        ..apply(target)

        target.clearAggro()
        target.addAggro(caster, caster.getBaseDamage(0) * 10.)


@compiletime function genAbility()
  var tooltip = new AbilityTooltipGenerator(Targettype.NONE, "Hero summons his loyal companion.")
  new ChannelAbilityPreset(COMPANION_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetButtonPosNormal(1, 1)
  ..presetButtonPosResearch(1, 0)
  ..presetIcon(Icons.bTNWolf)
  ..presetHotkey("D")

  ..tooltipStartListen()
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..setName("Companion")
  ..presetManaCost(lvl -> 90)
  ..presetCooldown(lvl -> 180)
  ..tooltipStopListen()

  
  new UnitDefinition(COMPANION_UNIT_ID, UnitIds.timberwolf)
  ..setName("Hachi")
  ..setAttack1CooldownTime(1.7)
  ..setHitPointsMaximumBase(300)
  ..setArmorType(ArmorType.Unarmored)
  ..setDefenseBase(2)
  ..setNormalAbilities(commaList(BITE_ID, CANNIBALIZE_ID))

  tooltip = new AbilityTooltipGenerator(Targettype.UNIT, "Hachi bites targeted unit and makes it |cffd803bctagged|r. Also |cffb93939taunts|r the target.")
  new ChannelAbilityPreset(BITE_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetButtonPosNormal(0, 2)
  ..presetIcon(Icons.bTNRedDragonDevour)
  ..presetHotkey("Q")
  ..presetTargetsAllowed(lvl -> TargetsAllowed.enemies)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.physical)
  ..presetTargetTypes(Targettype.UNIT)
  ..setName("Bite")
  ..addTooltipProperty("Damage ", lvl -> "Attack * " + BITE_DAMAGE_PER_ATTACK.toString(0))
  ..presetCastRange(lvl -> 256)
  ..presetManaCost(lvl -> 25)
  ..presetCooldown(lvl -> 8)
  ..tooltipStopListen()
  
  tooltip = new AbilityTooltipGenerator(Targettype.UNIT, "Hachi eats near corpse and restores health.")
  new AbilityDefinitionCannibalize(CANNIBALIZE_ID)
  ..registerTooltipGenerator(tooltip)
  ..presetButtonPosNormal(1, 2)
  ..presetHotkey("W")
  ..setRequirements("")
  ..setEffectSound(Sounds.cannibalize)
  ..setAnimationNames("attack1")

  ..tooltipStartListen()
  ..setName("Cannibalize")
  ..presetHitPointsperSecond(lvl -> 50)
  ..presetManaCost(lvl -> 15)
  ..presetCooldown(lvl -> 30)
  ..tooltipStopListen()
  
  ..presetDurationNormal(lvl -> 20)
  ..presetDurationHero(lvl -> 20)
  ..presetMaxHitPoints(lvl -> 10000)
