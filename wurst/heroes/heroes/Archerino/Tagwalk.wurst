package Tagwalk
import AbilityTooltipGenerator
import Icons
import PresetBuffs
import Abilities
import AttachmentPoints
import ClosureEvents
import GameConstants
import TooltipFactory

public constant TAGWALK_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure WALK_DURATION = lvl -> 5. + 1. * lvl

public constant tagBuffObj = compiletime(createDummyBuffObject("Tagged", "This unit has been tagged.", Icons.bTNTheBlackArrow, Abilities.parasiteTarget, AttachmentPoints.overhead))
constant stabBuffObj = compiletime(createDummyBuffObject("Tag Walk".color(POSITIVE_COLOR), "Increases speed and reduces damage taken. Enemies touched during the effect are |cffd803bctagged|r and will receive more damage.", Icons.bTNTheBlackArrow))

public class TagBuff extends NormalBuff

    construct(real duration)
        super(duration, tagBuffObj)

class TagwalkBuff extends NormalBuff
    let trig = CreateTrigger()
    effect windEff = null

    construct(real duration)
        super(duration, stabBuffObj)

    override function apply(unit target)
        super.apply(target)
        windEff = target.addEffect(Abilities.tornadoElementalSmall, AttachmentPoints.chest)..setScale(0.25)
        trig..registerUnitInRangeSource(target, 72., null)
        ..addAction() ->
            let inRange = GetTriggerUnit()
            let source = GetTriggeringTrigger().getSource()
            if inRange.isEnemyOf(source.getOwner())
                new TagBuff(15.)
                ..apply(inRange)
        

    override function defenseModifier()
        DamageEvent.setAmount(DamageEvent.getAmount() * 0.25)

    override function onEnd()
        super.onEnd()
        windEff.destr()
        target.clearRegister()
        trig.destr()


init
    EventListener.onCast(TAGWALK_ID) caster ->
        let lvl = caster.getAbilityLevel(TAGWALK_ID)

        new TagwalkBuff(WALK_DURATION.run(lvl))
        ..apply(caster)


@compiletime function genAbility()
    let tooltip = new AbilityTooltipGenerator("Reduces incoming damage and boosts speed. Walking through enemies will make them |cffd803bctagged|r, enhancing other spells.")
    new AbilityDefinitionBladeMasterWindWalk(TAGWALK_ID)
    ..registerTooltipGenerator(tooltip)
    ..presetIcon(Icons.bTNAmbush)
    ..presetButtonPosNormal(1, 2)
    ..presetButtonPosResearch(1, 0)
    
    ..tooltipStartListen()
    ..setLevels(NORMAL_SPELL_LEVELS)
    ..presetHotkey("W")
    ..setName("Tag Walk")
    ..addTooltipProperty("Walk Duration", WALK_DURATION)
    ..addTooltipProperty("Damage Reduction", lvl -> "75%")
    
    ..presetManaCost(lvl -> 40 + 10 * lvl)
    ..presetCooldown(lvl -> 20. - 2. * lvl)
    ..tooltipStopListen()
    
    ..presetDurationHero(WALK_DURATION)