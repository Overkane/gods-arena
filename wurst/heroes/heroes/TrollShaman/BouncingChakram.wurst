package BouncingChakram
import AbilityTooltipGenerator
import Icons
import HeroSpellConstants
import ClosureEvents
import UpgradeObjEditing
import PassiveAbilityPreset

public constant BOUNCING_CHAKRAM_ID = compiletime(ABIL_ID_GEN.next())
public constant BOUNCING_CHAKRAM_UPGRADE_ID = compiletime(UPGD_ID_GEN.next())

constant IntLevelClosure NUMBER_OF_BOUNCES = lvl -> lvl

init
  EventListener.add(EVENT_PLAYER_HERO_SKILL) -> 
    let u = GetTriggerUnit()
  
    if GetLearnedSkill() == BOUNCING_CHAKRAM_ID
      let lvl = u.getAbilityLevel(BOUNCING_CHAKRAM_ID)
      SetPlayerTechResearchedSwap(BOUNCING_CHAKRAM_UPGRADE_ID, lvl, GetTriggerPlayer())

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(Targettype.PASSIVE, "Hero attack can bounce between enemies, but each bounce reduces attack damage.")
  new PassiveAbilityPreset(BOUNCING_CHAKRAM_ID, NORMAL_SPELL_LEVELS, tooltip)
  ..presetIcon(Icons.pASBTNUpgradeMoonGlaive)
  ..presetButtonPosNormal(2, 2)
  ..presetButtonPosResearch(2, 0)
  ..presetHotkey("E")
  
  ..tooltipStartListen()
  ..setName("Bouncing Chakram")
  ..addTooltipProperty("Number of Bounces", NUMBER_OF_BOUNCES)
  ..addTooltipProperty("Damage Reduction per Bounce", lvl -> "15%")
  ..tooltipStopListen()

  new UpgradeDefinition(BOUNCING_CHAKRAM_UPGRADE_ID)
  ..setLevels(NORMAL_SPELL_LEVELS)
  ..addEffectAttackTargetCountBonus(1, 1)