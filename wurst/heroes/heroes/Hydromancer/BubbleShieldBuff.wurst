package BubbleShieldBuff
import public PresetBuffs
import Assets
import SoundUtils
import WaterElemental
import BonusHandler
import TooltipFactory
import CustomHeal

constant buffTuple = compiletime(createDummyBuffObject("Bubble Shield".color(POSITIVE_COLOR), "Increases unit's hp regeneration and can block small amount of damage",  Icons.bTNNeutralManaShield, Abilities.manaShieldCaster))

public class BubbleShieldBuff extends ShieldBuff
  real healAmount
  var bonusDamage = 0.
  unit caster

  private constant ON_APPLY_SOUND = new SoundDefinition(Sounds.manaShieldCaster1)
  
  construct(unit caster, real dur, real shieldDurabiblity, real healAmount)
    super(dur, buffTuple, shieldDurabiblity)
    this.healAmount = healAmount
    this.caster = caster

  override function onApply()
    if ELEM_LIST.has(target.getTypeId())
      bonusDamage = target.getBaseDamage(0).toReal()
      target.addBonus(Bonus.DAMAGE, bonusDamage)
    ON_APPLY_SOUND.play()

  override function onEnd()
    if bonusDamage != 0
      target.addBonus(Bonus.DAMAGE, -bonusDamage)

  override function update()
    super.update()
    caster.healTarget(target, healAmount, false)
    