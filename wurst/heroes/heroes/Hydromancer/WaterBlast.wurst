package WaterBlast
import AbilityTooltipGenerator
import GameConstants
import ClosureEvents
import ClosureForGroups
import SoundUtils
import WaterBlastBuff
import MapModels
import WaterElemental
import TooltipFactory
import CustomHeal
import DamageSystem

public constant WATER_BLAST_ID = compiletime(ABIL_ID_GEN.next())

constant SOUND = new SoundDefinition(Sounds.crushingWaveTarget1)

constant RealLevelClosure DAMAGE = lvl -> 85. + 85. * (lvl-1)
constant RealLevelClosure BONUS_INT_DAMAGE = lvl -> 0.5 * lvl
constant RealLevelClosure SLOW = lvl -> 30. + 5. * (lvl-1)
constant RealLevelClosure SLOW_DURATION = lvl -> 2.5 + 0.5 * lvl
constant RealLevelClosure AOE = lvl -> 300.

init 
  EventListener.onPointCast(WATER_BLAST_ID) (unit caster, vec2 target) ->
    let lvl = caster.getAbilityLevel(WATER_BLAST_ID)
    let spellDamage = DAMAGE.run(lvl) + BONUS_INT_DAMAGE.run(lvl) * caster.getInt(true)
    let spellAOE = AOE.run(lvl)
    let buffSlow = SLOW.run(lvl)
    let buffDuration = SLOW_DURATION.run(lvl)

    SOUND.play(1, 40)
    forUnitsInRange(target, spellAOE) (unit u) ->
      if u.isAlive() and not u.isAllyOf(caster.getOwner())
        new WaterBlastBuff(buffDuration, buffSlow).apply(u)
        caster.dealMagicalDamage(u, spellDamage)
      else if u.isAlive() and WATER_ELEM_LIST.has(u.getTypeId()) and u.isAllyOf(caster.getOwner())
        new WaterBlastBuff(buffDuration, -buffSlow).apply(u)
        caster.healTarget(u, spellDamage)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Deals damage to enemies in small radius and slows them. Heals and speed up ally water elementals instead.")
  new ChannelAbilityPreset(WATER_BLAST_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..presetIcon(Icons.bTNOrbOfFrost)
  ..presetButtonPosNormal(0, 2)
  ..presetButtonPosResearch(0, 0)
  ..presetHotkey("Q")
  ..setArtEffect(MapModels.waterBlast)
  ..setEffectSound(Sounds.crushingWaveTarget1)
  ..presetOption(Option.TARGETIMAGE, true)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..presetTargetTypes(Targettype.POINT)
  ..setName("Water Blast")
  ..addTooltipProperty("Damage", DAMAGE)
  ..addTooltipProperty("Bonus Intelligence Damage", lvl -> (BONUS_INT_DAMAGE.run(lvl)*100).percRound())
  ..addTooltipProperty("Slow", lvl -> SLOW.run(lvl).toString())
  ..addTooltipProperty("Duration", SLOW_DURATION)
  ..presetAreaofEffect(AOE)
  ..presetCastRange(lvl -> 600)
  ..presetManaCost(lvl -> 80 + 15 * lvl)
  ..presetCooldown(lvl -> 8.)
  ..tooltipStopListen()