package AquaBlast
import AbilityTooltipGenerator
import GameConstants
import ClosureEvents
import ClosureForGroups
import SoundUtils
import AquaBlastBuff
import MapModels
import WaterElemental
import CustomHeal
import DamageSystem
import AbilityDefinitionExtension

public constant AQUA_BLAST_ID = compiletime(ABIL_ID_GEN.next())

constant SOUND = new SoundDefinition(Sounds.crushingWaveTarget1)

constant RealLevelClosure DAMAGE = lvl -> 90. + 105. * (lvl-1)
constant RealLevelClosure SLOW = lvl -> 30. + 5. * (lvl-1)
constant RealLevelClosure SLOW_DURATION = lvl -> 2.5 + 0.5 * lvl
constant RealLevelClosure AOE = lvl -> 275.

init 
  EventListener.onPointCast(AQUA_BLAST_ID) (unit caster, vec2 target) ->
    let lvl = caster.getAbilityLevel(AQUA_BLAST_ID)
    let spellDamage = DAMAGE.run(lvl)
    let spellAOE = AOE.run(lvl)
    let buffSlow = SLOW.run(lvl)
    let buffDuration = SLOW_DURATION.run(lvl)

    SOUND.play(1, 40)
    forUnitsInRange(target, spellAOE) (unit u) ->
      if u.isAlive() and not u.isAllyOf(caster.getOwner())
        new AquaBlastBuff(buffDuration, buffSlow).apply(u)
        caster.dealMagicalDamage(u, spellDamage)
      else if u.isAlive() and ELEM_LIST.has(u.getTypeId()) and u.isAllyOf(caster.getOwner())
        new AquaBlastBuff(buffDuration, -buffSlow).apply(u)
        caster.healTarget(u, spellDamage)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Deals damage to enemies in small radius and slows them. Heals and speeds up |cff3640b5water elementals|r instead.")
  new ChannelAbilityPreset(AQUA_BLAST_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..makeQAbility()
  ..presetIcon(Icons.bTNOrbOfFrost)
  ..setArtEffect(MapModels.waterBlast)
  ..setEffectSound(Sounds.crushingWaveTarget1)
  ..presetOption(Option.TARGETIMAGE, true)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..presetTargetTypes(Targettype.POINT)
  ..setName("Aqua Blast")
  ..addTooltipProperty("Damage", DAMAGE)
  ..addTooltipProperty("Slow", lvl -> SLOW.run(lvl).toString())
  ..addTooltipProperty("Duration", SLOW_DURATION)
  ..presetAreaofEffect(AOE)
  ..presetCastRange(lvl -> 600)
  ..presetManaCost(lvl -> 80 + 15 * lvl)
  ..presetCooldown(lvl -> 8.)
  ..tooltipStopListen()