package Obliterator
import AbilityTooltipGenerator
import Buff
import DemonLordIds
import ObliteratorBuff
import PassiveAbilityPreset
import GameConstants
import TooltipFactory
import AbilityDefinitionExtension

constant RealLevelClosure BONUS_ATTACK_SPEED = lvl -> 50. + 25 * (lvl-1)
constant RealLevelClosure CREEP_AGGRO = lvl -> 600.
constant RealLevelClosure CREEP_AGGRO_RANGE = lvl -> 400.
constant RealLevelClosure BONUS_HEALTH_THRESHOLD = lvl -> .35
constant RealLevelClosure SLAY_RATIO = lvl -> 0.05 + 0.02 * (lvl-1)
constant RealLevelClosure SLAY_BONUS_DAMAGE_MULTIPLIER = lvl -> 10.

init
  DamageEvent.addListener() ->
    let target = DamageEvent.getTarget()
    let source = DamageEvent.getSource()
    if target.hasAbility(OBLITERATOR_ID)
      let lvl = target.getAbilityLevel(OBLITERATOR_ID)

      if target.getHPRatio() < BONUS_HEALTH_THRESHOLD.run(lvl)
        new ObliteratorBuff(
          BONUS_HEALTH_THRESHOLD.run(lvl),
          CREEP_AGGRO_RANGE.run(lvl),
          CREEP_AGGRO.run(lvl),
          BONUS_ATTACK_SPEED.run(lvl)
        ).apply(target)
      else if source.hasBuff(ObliteratorBuff.typeId) and target.getHPRatio() < SLAY_RATIO.run(lvl)
        DamageEvent.addAmount(DamageEvent.getAmount() * (SLAY_BONUS_DAMAGE_MULTIPLIER.run(lvl) - 1.))

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator(
    Targettype.PASSIVE,
    "{0} passively grants bonus attack speed, nearby creep aggro, and bonus damage against low HP units."
    .format("On low HP".color(IMPORTANT_COLOR))
  )
  new PassiveAbilityPreset(OBLITERATOR_ID, INNATE_SPELL_LEVELS, tooltip)
  ..makeDAbility()
  ..setIconNormal(Icons.pASBTNFireRocks)
  
  ..tooltipStartListen()
  ..addTooltipProperty("Bonus Attack Speed", BONUS_ATTACK_SPEED)
  ..addTooltipProperty("Creep Aggro", CREEP_AGGRO)
  ..addTooltipProperty("Creep Aggro Range", CREEP_AGGRO_RANGE)
  ..addTooltipProperty("Health Threshold", lvl -> (BONUS_HEALTH_THRESHOLD.run(lvl) * 100).percRound())
  ..addTooltipProperty("Bonus Damage HP Threshold", lvl -> (SLAY_RATIO.run(lvl) * 100).percRound())
  ..addTooltipProperty("Bonus Damage Multiplier", SLAY_BONUS_DAMAGE_MULTIPLIER)
  ..setName("Obliterator")
  ..tooltipStopListen()
