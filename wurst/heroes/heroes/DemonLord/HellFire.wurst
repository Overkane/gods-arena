package HellFire
import AbilityTooltipGenerator
import GameConstants
import DemonLordIds
import HellFireBuff
import TooltipFactory
import ClosureEvents
import DamageSystem
import AbilityDefinitionExtension

init
  EventListener.onCast(HELL_FIRE_ID) (unit caster) ->
    let lvl = caster.getAbilityLevel(HELL_FIRE_ID)

    new HellFireBuff(lvl).apply(caster)

    caster..hideAbility(HELL_FIRE_ID, true)
    ..addAbility(HELL_FIRE_TURN_OFF_ID, lvl)

  EventListener.onCast(HELL_FIRE_TURN_OFF_ID) (unit caster) ->
    if caster.hasBuff(HellFireBuff.typeId)
      caster.getBuff(HellFireBuff.typeId).terminate()

    caster..removeAbility(HELL_FIRE_TURN_OFF_ID)
    ..hideAbility(HELL_FIRE_ID, false)

  EventListener.add(EVENT_PLAYER_HERO_SKILL) ->
    let u = GetTriggerUnit()

    if EventData.getLearnedSkill() == HELL_FIRE_ID
      and u.hasAbility(HELL_FIRE_TURN_OFF_ID)

      let lvl = u.getAbilityLevel(HELL_FIRE_ID)
      
      // Reapplying buff to refresh properties, since reapplying doesn't work.
      u.getBuff(HellFireBuff.typeId).terminate()
      new HellFireBuff(lvl).apply(u)

      u.incAbilityLevel(HELL_FIRE_TURN_OFF_ID)

  EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
    let u = GetTriggerUnit()

    if EventData.getTriggerUnit().hasAbility(HELL_FIRE_TURN_OFF_ID)

      u..removeAbility(HELL_FIRE_TURN_OFF_ID)
      ..hideAbility(HELL_FIRE_ID, false)

@compiletime function genAbility()
  var tooltip = new AbilityTooltipGenerator(Targettype.NONE, "On activation deals periodic damage to enemies nearby based on the Demon Lord max HP. "
    + "Can be deactivated anytime. {0}.".format("Damage, done by ability to caster, is lethal".color(IMPORTANT_COLOR)))
  new ChannelAbilityPreset(HELL_FIRE_ID, ULTIMATE_SPELL_LEVELS, true, tooltip)
  ..makeRAbility()
  ..presetIcon(Icons.bTNImmolationOn)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..setName("Hell Fire")
  ..addTooltipProperty("HP % Drain", lvl -> HELL_FIRE_HP_DRAIN.run(lvl).percRound(1))
  ..addTooltipProperty("Damage per HP Drained", lvl -> HELL_FIRE_DAMAGE_PER_DRAINED_HP.run(lvl))
  ..presetAreaofEffect(lvl -> HELL_FIRE_AOE)
  ..presetManaCost(lvl -> 90)
  ..tooltipStopListen()

  tooltip = new AbilityTooltipGenerator(Targettype.NONE, "On activation deals periodic damage to enemies nearby based on the Demon Lord max HP. "
    + "Can be deactivated anytime. {0}.".format("Damage, done by ability to caster, is lethal".color(IMPORTANT_COLOR)))
  new ChannelAbilityPreset(HELL_FIRE_TURN_OFF_ID, ULTIMATE_SPELL_LEVELS, true, tooltip)
  ..makeRAbility()
  ..presetIcon(Icons.bTNImmolationOff)

  ..tooltipStartListen()
  ..addTooltipProperty("Damage Type", DamageTypeTooltips.magical)
  ..setName("Hell Fire")
  ..addTooltipProperty("HP % Drain", lvl -> HELL_FIRE_HP_DRAIN.run(lvl).percRound(1))
  ..addTooltipProperty("Damage per HP Drained", lvl -> HELL_FIRE_DAMAGE_PER_DRAINED_HP.run(lvl))
  ..presetAreaofEffect(lvl -> HELL_FIRE_AOE)
  ..tooltipStopListen()
