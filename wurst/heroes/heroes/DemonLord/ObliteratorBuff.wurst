package ObliteratorBuff

import Assets
import BonusHandler
import ClosureForGroups
import CreepAggro
import PresetBuffs
import TooltipFactory

constant buffTupleBuff = compiletime(
  createDummyBuffObject(
    "Obliterator".color(POSITIVE_COLOR),
    "Grants bonus attack speed, nearby creep aggro, and bonus damage against low HP units",
    Icons.bTNFireRocks,
    Abilities.vampiricAura
  )
)

public class ObliteratorBuff extends NormalBuff
  static constant FOREVER = 9999999.
  real bonus_health_threshold
  real creep_aggro_range
  real creep_aggro
  real bonus_attack_speed
  construct(real bonus_health_threshold, real creep_aggro_range, real creep_aggro, real bonus_attack_speed)
    super(FOREVER, buffTupleBuff)

    this.bonus_health_threshold = bonus_health_threshold
    this.creep_aggro_range = creep_aggro_range
    this.creep_aggro = creep_aggro
    this.bonus_attack_speed = bonus_attack_speed

  override function update()
    super.update()
    if this.target.getHPRatio() > this.bonus_health_threshold
      terminate()
    if not done
      forUnitsInRange(this.target.getPos(), this.creep_aggro_range) u ->
        if u.isEnemyOf(this.target)
          this.target.maxAggro(u, this.creep_aggro)

  override function onApply()
    this.target.addBonus(Bonus.ATTACKSPEED, this.bonus_attack_speed)

  override function onEnd()
    if this.target != null
      this.target.addBonus(Bonus.ATTACKSPEED, -this.bonus_attack_speed)