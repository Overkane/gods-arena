package BlinkStrike
import AbilityTooltipGenerator
import GameConstants
import TooltipFactory
import ClosureEvents
import ClosureTimers
import BlinkStrikeBuff
import Buff
import Abilities
import OrderIds
import AbilityDefinitionExtension

public constant BLINK_STRIKE_ID = compiletime(ABIL_ID_GEN.next())

constant RealLevelClosure AGI_BONUS = lvl -> 0.1 + 0.1 * lvl
constant RealLevelClosure AGI_BUFF_DURATION = lvl -> 4. + 0.5 * lvl
constant RealLevelClosure BLINK_STRKE_DEBUFF_DURATION = lvl -> 3
constant BLINK_STRIKE_MAX_STACKS_AMOUNT = 3

init
  BlinkStrikeBuff.setMaxBuffAmount(BLINK_STRIKE_MAX_STACKS_AMOUNT)

  EventListener.onTargetCast(BLINK_STRIKE_ID) (unit caster, unit target) ->
    nullTimer() -> 
      let lvl = caster.getAbilityLevel(BLINK_STRIKE_ID)
      let bonusAgi = AGI_BONUS.run(lvl)
      let bonusAgiDuration = AGI_BUFF_DURATION.run(lvl)
      let debuffDuration = BLINK_STRKE_DEBUFF_DURATION.run(lvl)

      let facingAngle = target.getFacingAngle()
      let backAngle = facingAngle - (180).fromDeg()
      let backPos = target.getPos().polarOffset(backAngle, 24)
      
      caster
      ..setPos(backPos)
      ..setFacing(caster.getPos().angleTo(target.getPos()))
      flashEffect(Abilities.blinkCaster, caster.getPos())

      caster.issueTargetOrderById(OrderIds.attack, target)

      new BlinkStrikeBuff(bonusAgiDuration, bonusAgi).apply(caster)
      new BlinkStrikeDebuff(debuffDuration).apply(target)

  EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
    let killer = EventData.getKillingUnit()
    let diedUnit = EventData.getTriggerUnit()

    if killer.hasAbility(BLINK_STRIKE_ID) and diedUnit.hasBuff(BlinkStrikeDebuff.typeId) and diedUnit.isEnemyOf(killer.getOwner())
      killer.resetAbilityCooldown(BLINK_STRIKE_ID)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Assasin blinks behind the target unit and get a temporary agility buff which is based on base agility.\n"
    +"If the target unit is killed within " + ("Time to kill").color(COLOR_GOLD_STR) + " time, the ability cooldown is reset. Agility buff stacks, but number of stacks is limited.")
  new ChannelAbilityPreset(BLINK_STRIKE_ID, NORMAL_SPELL_LEVELS, true, tooltip)
  ..makeWAbility()
  ..presetIcon(Icons.bTNShadowMeld)
  ..presetTargetsAllowed(lvl -> TargetsAllowed.enemies)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.UNIT)
  ..setName("Blink Strike")
  ..addTooltipProperty("Agility Bonus", lvl -> (AGI_BONUS.run(lvl) * 100).percRound())
  ..addTooltipProperty("Agilitiy Buff Duration", AGI_BUFF_DURATION)
  ..addTooltipProperty("Time to kill", BLINK_STRKE_DEBUFF_DURATION)
  ..addTooltipProperty("Max Stacks Amount", lvl -> BLINK_STRIKE_MAX_STACKS_AMOUNT)
  ..presetCastRange(lvl -> 800)
  ..presetManaCost(lvl -> 45 + 25 * (lvl-1))
  ..presetCooldown(lvl -> 22)
  ..tooltipStopListen()
