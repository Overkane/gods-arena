package Contract
import AbilityTooltipGenerator
import ClosureEvents
import ClosureForGroups
import LinkedList
import ContractBuff
import DamageSystem
import Buff
import TooltipFactory
import GeneralFunctions
import GameConstants
import AbilityDefinitionExtension

public constant CONTRACT_ID = compiletime(ABIL_ID_GEN.next())

constant AOE = 500.
constant DURATION = 15.
constant BOUNTY = 4
constant RealLevelClosure BONUS_AP = lvl -> 8. * lvl
constant RealLevelClosure COOLDOWN = lvl -> 70. - 10 * lvl

init 
  EventListener.onCast(CONTRACT_ID) (unit caster) ->
    let targets = new LinkedList<unit>

    forUnitsInRange(caster.getPos(), AOE) (unit u) ->
      if u.isEnemyOf(caster) and u.isAlive()
        targets.add(u)  

    if not targets.isEmpty()
      targets.shuffle()
      let target = targets.pop()

      new ContractBuff(DURATION).apply(target)
    else 
      caster.resetAbilityCooldown(CONTRACT_ID)

    destroy targets
  
  EventListener.add(EVENT_PLAYER_UNIT_DEATH) ->
    let sunit = EventData.getKillingUnit()
    let tunit = EventData.getDyingUnit()
    
    if tunit.hasBuff(ContractBuff.typeId) 
      if sunit.hasAbility(CONTRACT_ID)
        let bounty = BOUNTY * sunit.getLevel()
        
        sunit.getOwner().addGold(bounty)
        
        createBountyTextTag(tunit, bounty, sunit.getOwner(), 60)
      else
        forUnitsInRange(tunit.getPos(), 225) (unit u) ->
          if u.hasAbility(CONTRACT_ID)
            let bounty = BOUNTY * u.getLevel()
        
            u.getOwner().addGold(bounty)
            createBountyTextTag(tunit, bounty, u.getOwner(), 60)
      
  DamageEvent.addUnreducedListener(DamageEvents.START castTo int) ->
    let sunit = DamageEvent.getSource()
    let tunit = DamageEvent.getTarget()

    if tunit.hasBuff(ContractBuff.typeId) and sunit.hasAbility(CONTRACT_ID)
      let lvl = sunit.getAbilityLevel(CONTRACT_ID)
      let bonusAP = BONUS_AP.run(lvl)
      sunit.addOnetimeStat(CustomUnitStats.ATTACK_POWER, bonusAP/100)

@compiletime function genAbility()
  let tooltip = new AbilityTooltipGenerator("Assassin takes a killing contract on a random target in the AOE. " 
  + "She deals additional damage to the contract target and if she kills it will get additional bounty. "
  + "If she doesn't kill the target, but is in 225 range of it, then Assassin will get half of the bounty, otherwise nothing. "
  + "If there is no appropriate target nearby, the spell cooldown will be reset.")
  new ChannelAbilityPreset(CONTRACT_ID, INNATE_SPELL_LEVELS, true, tooltip)
  ..makeDAbility()
  ..presetIcon(Icons.bTNSnazzyScrollPurple)

  ..tooltipStartListen()
  ..presetTargetTypes(Targettype.NONE)
  ..setName("Contract")
  ..addTooltipProperty("Bonus Physical Damage", lvl -> BONUS_AP.run(lvl).percRound())
  ..addTooltipProperty("Additional Bounty", lvl -> BOUNTY.toString() + " * [Hero Level]")
  ..presetAreaofEffect(lvl -> AOE)
  ..presetCooldown(COOLDOWN)
  ..tooltipStopListen()
  
  ..presetManaCost(lvl -> 0)
